
1000 GOSUB3500:GOTO20:REM KASBOEK
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="RENTE-BEREKENING":GOSUB3500
1030 DIMN$(NR)
1050 GOSUB3600
1060 IFCR=1 THENDIMA(NR,15)
1800 GOTO4000
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N2$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE + CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2830
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2500 REM CONTROLE BANKREK.NO.
2510 IFLEN(RK$)<>9 THENM=1:GOTO2930
2520 Q=0:FORI=1 TOLEN(RK$)
2530 Q=Q+VAL(MID$(RK$,I,1))*(10-I):NEXTI
2540 IF(Q/11)=INT(Q/11) THENRETURN
2550 M=1:GOTO2910
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2850 PRINT"Graag een datum NA '010170'":GOTO4400
2860 GOSUB250:PRINT"U deed iets verkeerd!":GOTO4400
2870 GOSUB250
2880 PRINT"Moet in hetzelfde jaar zijn!":GOTO4400
2890 GOSUB250:PRINT"tenminste 55 kar.!":RETURN
2900 GOSUB250:PRINT"tenminste 20 regels!":RETURN
2910 GOSUB250
2920 PRINT"Dit is geen Bankrekeningnummer!":GOTO4400
2930 GOSUB250
2940 PRINT"Bankrek.nummer = 9 cijfers!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3200 REM STRING UITEENRAFELEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDGN
3230 M3$=MID$(L$,11,4):REM K/B/C/P
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 RETURN
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR
3520 A=(45+3)*NR:REM POSTEN
3530 A=A+(16+3)*RB:REM RUBRIEKEN
3540 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB100:N$="INTEGRALE HUISHOUD-REKENING":VE=0
3620 GOSUB3000:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
4000 GOSUB2150:PRINT:BG=0:BK=0:T1=0
4010 PRINT" 1=Aanvullende Toelichting":PRINT
4020 PRINT" 2=Rente-Berekening":PRINT
4030 PRINT" 3=Einde":PRINT:PRINT
4040 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4050 W=VAL(IN$):IF(W<1) OR(W>3) THEN4000
4060 ONW GOTO9000,5000,10000
4400 REM CORRECTIE
4410 FORI=1 TO1000:NEXTI:HO=0:GOSUB110:GOSUB4440
4420 VE=VE+1:GOSUB110:GOSUB4440
4430 VE=VE+1:GOSUB110:GOSUB4440:VE=VE-3:RETURN
4440 PRINT"                                       ":RETURN
4500 REM ONDERKANT SCHERM SCHOON
4510 GOSUB120:T2=VE:HO=0
4520 FORI=16 TOT2:VE=I-1:GOSUB110:GOSUB4440
4530 NEXTI:RETURN
5000 N=0:GOSUB20000:IFN=0 THEN4000
5010 GOSUB8400:IFBG=0 THENBG=1:BS=VAL(TS$)
5020 IFBK=0 THENBK=1:GOTO5040
5030 GOTO5400
5040 GOSUB2170:PRINT
5050 PRINT"Over welke periode? :"
5060 GOSUB250:HO=0:VE=7:GOSUB110
5070 PRINT"vanaf datum (DDMMJJ): ";:INPUTD1$
5080 L$=D1$:GOSUB2200:IFM=1 THENM=0:GOTO5060
5090 D1=SS:JR=VAL(RIGHT$(D1$,2))
5100 GOSUB250:HO=0:VE=9:GOSUB110
5110 PRINT"t/m   datum (DDMMJJ): ";:INPUTD2$
5120 L$=D2$:GOSUB2200:IFM=1 THENM=0:GOTO5100
5130 D2=SS:JJ=VAL(RIGHT$(D2$,2))
5140 IFJJ<>JR THENGOSUB2870:GOTO5040
5150 JD=365:IF(JJ/4=INT(JJ/4)) THENJD=366
5160 IFD1>D2 THENGOSUB5240
5170 IF(D1<TE) AND(D2<TE) THEN6550
5180 IF(D1>TL) AND(D2>TL) THEN6550
5190 GOSUB250:HO=0:VE=14:GOSUB110
5200 PRINT"Wilt U de berekening zien? (J/N): ";
5210 GOSUB210:PRINT:GOSUB23000
5220 CR=0:IFIN$="J" THENCR=1:GOTO5300
5230 PT=0:GOTO5380
5240 D$=D1$:D1$=D2$:D2$=D$
5250 D=D1:D1=D2:D2=D:RETURN
5300 GOSUB2150:PRINT
5310 PRINT" 1=video-prints (op scherm)":PRINT
5320 PRINT" 2=print-outs (met printer)":PRINT
5330 PRINT:PRINT
5340 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
5350 W=VAL(IN$):IF(W<1) OR(W>2) THEN5300
5360 PT=0:RT=0:RT$="":RL=39:AR=22
5370 IFW=2 THENPT=5:GOSUB8000
5380 M=0:A=0:BL=0:RR=0
5390 RC=0:RD=0:B1=0:B2=0:E=0
5400 IF(CR=0) OR(PT=5) THENGOSUB8600
5410 IFCR=1 THENGOSUB7300
5500 FL=0:FORI=1 TON:IFFL=1 THEN5590
5510 L$=N$(I):GOSUB3200
5520 IFM4$="0" THENN$(I)="":GOTO5590
5530 IFM4$="3" THENFL=1:GOTO5590
5540 L$=M1$:GOSUB2200
5550 IFSS<D1 THENGOSUB6510:GOTO5590
5560 IFSS>D2 THENN$(I)="":GOTO5590
5570 IFBG=1 THENBG=2:GOSUB6700:GOTO5590
5580 GOSUB7000
5590 NEXTI:IFFL=1 THENFL=0:GOSUB6610:GOTO4000
5600 M=0:IFSS<D2 THENGOSUB8300
5610 IFM=5 THENM=0:GOTO5000
5620 GOSUB6520:IFCR=0 THEN5640
5625 L$=L4$:GOSUB7800:RR=0
5630 IF(PT=0) AND(CR=1) THENGOSUB3100
5640 SR=RC:CT=7:CN=3:GOSUB310:RC$=SR$
5650 SR=-RD:GOSUB310:RD$=SR$
5660 L1$="Credit Rentenummer: "+RC$
5670 L2$="Debet  Rentenummer: "+RD$
5680 IFPT=5 THENL$=RT$+L1$:GOSUB7800
5690 IFPT=5 THENL$=RT$+L2$:GOSUB7800
5700 GOSUB2170:GOSUB250
5710 HO=0:VE=6:GOSUB110:PRINTL1$
5720 HO=0:VE=8:GOSUB110:PRINTL2$
5730 GOSUB250:VE=12:GOSUB110
5740 PRINT"Wat is bekend? :":PRINT
5750 PRINT" 1=rente-percentages":PRINT
5760 PRINT" 2=rente-bedragen":PRINT
5770 PRINT"(3=terug naar Hoofd-Menu)":PRINT:PRINT
5780 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
5790 W=VAL(IN$):IF(W<1) OR(W>3) THEN5700
5800 IFW=3 THENGOSUB270:GOTO4000
5810 ONW GOTO5900,6300
5900 GOSUB2170:PRINT:PRINT
5910 PRINT"Geef de percentages (niet de fracties):"
5920 GOSUB250:HO=0:VE=8:GOSUB110
5930 PRINT"Credit-rente ............: "
5940 PRINT"(iets meer dan niets)"
5950 HO=28:VE=8:GOSUB110:INPUTCR$
5960 IFCR$="" THEN5920
5970 CR=VAL(CR$)
5980 GOSUB250:HO=0:VE=11:GOSUB110
5990 PRINT"Debet- rente ............: "
6000 PRINT"(iets minder dan alles)"
6010 HO=28:VE=11:GOSUB110:INPUTDR$
6020 SR=RC*VAL(CR$):CT=7:CN=2:GOSUB310
6030 CR$=SR$:CR=VAL(CR$)
6040 L$="U ontvangt ..............:FL. "+CR$
6050 HO=0:VE=15:GOSUB110:PRINTL$
6060 SR=-RD*VAL(DR$):CT=7:CN=2:GOSUB310
6070 DR$=SR$:DR=VAL(DR$)
6080 L$="U betaalt ...............:FL. "+DR$
6090 HO=0:VE=17:GOSUB110:PRINTL$
6100 L$="Per saldo ":IF(CR-DR)<0 THEN6160
6110 SR=CR-DR:GOSUB310
6120 A$="ontvangt U ....:FL. "+SR$:L$=L$+A$
6130 HO=0:VE=19:GOSUB110:PRINTL$
6140 PRINT:PRINT"      *** Alle beetjes helpen! ***"
6150 GOSUB3100:GOTO5700
6160 SR=-(CR-DR):GOSUB310
6170 A$="betaalt U .....:FL. "+SR$:L$=L$+A$
6180 HO=0:VE=19:GOSUB110:PRINTL$
6190 PRINT:PRINT"      *** Na ons de overvloed! ***"
6200 GOSUB3100:GOTO5700
6300 GOSUB2170:PRINT:PRINT
6310 PRINT"Geef de rente-bedragen:"
6320 GOSUB250:HO=0:VE=8:GOSUB110
6330 PRINT"Credit-rente: FL. ";:INPUTCR$
6340 IFCR$="" THENGOSUB2810:GOTO6320
6350 CR=VAL(CR$)
6360 GOSUB250:HO=0:VE=10:GOSUB110
6370 PRINT"Debet- rente: FL. ";:INPUTDR$
6380 IFDR$="" THENGOSUB2810:GOTO6360
6390 DR=VAL(DR$)
6400 IFRC=0 THENSR=0:GOTO6420
6410 SR=CR/RC
6420 CT=7:CN=3:GOSUB310:CR$=SR$
6430 L$="Percentage Credit-rente: "+SR$
6440 HO=0:VE=16:GOSUB110:PRINTL$
6450 IFRD=0 THENSR=0:GOTO6470
6460 SR=DR/(-RD)
6470 GOSUB310:DR$=SR$
6480 L$="Percentage Debet- rente: "+SR$
6490 HO=0:VE=18:GOSUB110:PRINTL$
6500 GOSUB3100:GOTO5700
6510 BS=BS-VAL(M6$):N$(I)="":RETURN
6520 E=1:D4=D5:D5=D2:DG=D5-D4:GOSUB7200
6530 IFCR=0 THENRETURN
6540 GOTO7020
6550 GOSUB2150:FORK=1 TO3:GOSUB250:NEXTK
6560 HO=9:VE=12:GOSUB110
6570 PRINT"Over gevraagde periode"
6580 HO=6:VE=14:GOSUB110
6590 PRINT"geen posten in dit bestand!"
6600 GOSUB3100:GOSUB270:BG=0:BK=0:GOTO5010
6610 GOSUB2150:FORK=1 TO3:GOSUB250:NEXTK
6620 HO=0:VE=12:GOSUB110
6630 PRINT"In dit bestand zitten Kasboek-posten!":PRINT
6640 PRINT"Hierdoor is het saldo niet correct!"
6650 GOSUB3100:GOSUB270:GOTO4000
6700 D4=D1:D5=SS:DG=D5-D4:GOSUB7200
6710 IFCR=0 THENGOSUB6510:RETURN
6720 A$=D1$+" Begin -> "
6730 IFPT=5 THENGOSUB6960
6740 SR=BS:CT=9:CN=2:GOSUB310:BS$=SR$
6750 SR=DG:GOSUB300:DG$=SR$
6760 IFLEN(DG$)<3 THENDG$=" "+DG$:GOTO6760
6770 IFDG=0 THENRN$="       ":GOTO6800
6780 SR=RN:IFRN<0 THENSR=-RN
6790 CT=7:CN=3:GOSUB310:RN$=SR$
6800 IFBS>=0 THEND$="C"
6810 IFBS<0 THEND$="D"
6820 A$=A$+" "+BS$+" "+DG$
6830 IFPT=5 THENA$=A$+" "
6840 L$=RT$+A$+RN$+" "+D$:GOSUB7800
6850 GOSUB6510
6860 SR=BS:CT=9:CN=2:GOSUB310:BS$=SR$
6870 IFLEFT$(M3$,3)="PLA" THENPS=0:GOTO6910
6880 B1=VAL(RIGHT$(M3$,3))
6890 IFB1<>B2 THENPS=0
6900 PS=PS+1
6910 SR=PS:GOSUB300:PS$=SR$
6920 IFLEN(PS$)<3 THENPS$=" "+PS$:GOTO6920
6930 IFPT=5 THENGOSUB6980:GOTO6950
6940 A$=M1$+" "+M6$+" "+BS$+" "
6950 N$(I)="":B2=B1:B1=0:RETURN
6960 A$=D1$+" Begin ->            "
6970 RETURN
6980 A$=M1$+" "+M3$+" "+PS$+" "+M6$+" "
6990 A$=A$+M4$+" "+BS$+" ":RETURN
7000 D4=D5:D5=SS:DG=D5-D4:GOSUB7200
7010 IFCR=0 THENGOSUB6510:RETURN
7020 SR=DG:GOSUB300:DG$=SR$
7030 IFLEN(DG$)<3 THENDG$=" "+DG$:GOTO7030
7040 IFDG=0 THENRN$="       ":GOTO7070
7050 SR=RN:IFRN<0 THENSR=-RN
7060 CT=7:CN=3:GOSUB310:RN$=SR$
7070 IFBS>=0 THEND$="C"
7080 IFBS<0 THEND$="D"
7090 A$=A$+DG$
7100 IFPT=5 THENA$=A$+" "
7110 L$=RT$+A$+RN$+" "+D$:GOSUB7800
7120 IFE=1 THENE=0:RETURN
7130 GOTO6850
7200 REM BEREKENING RENTENUMMER
7210 IFBS>=10000 THEN7260
7220 RN=(DG*BS)/(JD*100)
7230 IFBS>=0 THENRC=RC+RN
7240 IFBS<0 THENRD=RD+RN
7250 RETURN
7260 RN=(DG*100)/JD:GOTO7230
7300 REM KOP
7310 BL=BL+1:IFPT=5 THEN7370
7320 GOSUB100:SR=BL:GOSUB300:L$="Blad:"+SR$
7330 L=RL-LEN(N2$):GOSUB7620:L$=N2$+L$
7340 GOSUB7800:L=39:GOSUB7640:L$=RT$+L$
7350 L4$=L$:GOSUB7800:GOSUB7500:L$=RT$+L$
7360 GOSUB7800:L$=L4$:GOSUB7800:RETURN
7370 L$=DT$:L=LL-LEN(RK$):GOSUB7620:L$=RK$+L$
7380 GOSUB7800:GOSUB7800:SR=BL:GOSUB300
7390 L$="Blad: "+SR$:L=LL:GOSUB7620:GOSUB7800
7400 L$=N1$:GOSUB7450:L$=N2$:GOSUB7450
7410 L$="Van "+D1$+" t/m "+D2$:M=5:GOSUB7450
7420 GOSUB7800:L=51:GOSUB7640:L$=RT$+L$
7430 L4$=L$:GOSUB7800:GOSUB7520:L$=RT$+L$
7440 GOSUB7800:L$=L4$:GOSUB7800:RETURN
7450 L1=LEN(L$):L2=INT((LL-L1)/2):L=L1+L2
7460 GOSUB7620:GOSUB7800
7470 IFM=5 THENM=0:RETURN
7480 L=L1:GOSUB7640:L=L1+L2:GOSUB7620
7490 GOSUB7800:RETURN
7500 L$="Datum   Mutatie    Saldo   Dgn Rentnr *"
7510 RETURN
7520 L$="Datum  BAfr Pst  Mutatie  T   "
7530 L$=L$+"Saldo   Dgn Rentenr *":RETURN
7600 IFLEN(L$)<L THENL$=L$+" ":GOTO7600
7610 RETURN
7620 IFLEN(L$)<L THENL$=" "+L$:GOTO7620
7630 RETURN
7640 IFLEN(L$)<L THENL$=L$+"-":GOTO7640
7650 RETURN
7800 REM PRINTEN
7810 IFPT=5 THEN7830
7820 PRINTL$:L$="":GOTO7850
7830 SR$=L$:GOSUB350:GOSUB360
7840 SR$="":L$=""
7850 RR=RR+1:IFRR=AR THENRR=0:GOTO7870
7860 RETURN
7870 L$=L4$:GOSUB7910
7880 IFPT=0 THENGOSUB3100
7890 IFPT=5 THENGOSUB8500:GOSUB8600
7900 GOSUB7300:RETURN
7910 IFPT=5 THEN7930
7920 PRINTL$:L$="":RETURN
7930 SR$=L$:GOSUB350:GOSUB360
7940 SR$="":L$="":RETURN
8000 N3$="Print-outs":GOSUB2170
8010 GOSUB250:HO=5:VE=8:GOSUB110
8020 PRINT"Hebt U een printer? (J/N): ";
8030 GOSUB210:PRINTIN$:GOSUB23000
8040 IFIN$="N" THEN5300
8050 FORI=1 TO3:GOSUB250:NEXTI:HO=8:VE=12
8060 GOSUB110:PRINT"Zet deze dan nu aan!"
8070 GOSUB250:HO=0:VE=18:GOSUB110
8080 PRINT"Aantal karakters op een regel: ";:INPUTX1
8090 IFX1<55 THENGOSUB2890:GOTO8070
8100 GOSUB250:HO=0:VE=20:GOSUB110
8110 PRINT"Aantal regels op een blad ...: ";:INPUTX2
8120 IFX2<20 THENGOSUB2900:GOTO8100
8130 GOSUB2170:GOSUB250:HO=0:VE=6:GOSUB110
8140 PRINT"Uw Bankrekeningnummer: ";:INPUTRK$
8150 IFRK$="" THENGOSUB2810:GOTO8130
8160 GOSUB2500:IFM=1 THENM=0:GOTO8130
8170 RK$="Bankrekeningnummer: "+RK$
8180 GOSUB250:VE=8:GOSUB110
8190 PRINT"Geef 'Datum vandaag' (voluit):"
8200 INPUTDT$:IFDT$="" THENGOSUB2810:GOTO8180
8210 LL=X1:AR=X2:RL=51:RT=INT((LL-RL)/2)
8220 IFLEN(RT$)<RT THENRT$=RT$+" ":GOTO8220
8230 RETURN
8300 IF(CR=1) THENGOSUB8380
8305 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
8310 PRINT" 1=de gewenste periode is met dit"
8320 PRINT"   bestand afgesloten,":PRINT
8330 PRINT" 2=nog een bestand inlezen.":PRINT
8340 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
8350 W=VAL(IN$):IF(W<1) OR(W>2) THEN8305
8360 IFW=2 THENM=5
8370 RETURN
8380 L$=L4$:GOSUB7800:IFPT=0 THENGOSUB3100
8385 RR=0:IFPT=5 THENGOSUB8500
8390 RETURN
8400 GOSUB2170:GOSUB250:HO=7:VE=8:GOSUB110
8410 PRINT"Bijgewerkt t/m    : ";TD$
8420 VE=10:GOSUB110
8430 PRINT"Datum eerste  post: ";TE$
8440 VE=12:GOSUB110
8450 PRINT"Datum laatste post: ";TL$
8460 VE=14:GOSUB110
8470 PRINT"Totaal aantal posten: ";N
8480 GOSUB3100:RETURN
8500 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
8510 HO=0:VE=12:GOSUB110
8520 PRINT"U moet nu een nieuw blad instellen!"
8530 GOSUB3100:RETURN
8600 GOSUB2170:GOSUB250:HO=13:VE=12:GOSUB110
8610 PRINT"Ik ben bezig!":RETURN
8800 REM BESTAND LEEGMAKEN
8810 FORI=1 TONR:N$(I)="":NEXTI:N=0
8820 GOSUB270:RETURN
9000 N3$="Aanvullende Toelichting":GOSUB2170:PRINT
9010 PRINT"De  rente-berekening  vindt  plaats  op"
9020 PRINT"basis van 'dag-rente' zoals voor prive-"
9030 PRINT"rekeningen gebruikelijk is.":PRINT
9040 PRINT"De 'DEBET'-rente (negatief saldo) en de"
9050 PRINT"'CREDIT'-rente (positief saldo)  worden"
9060 PRINT"apart berekend.":PRINT
9070 PRINT"De 'CREDIT'-rente wordt  berekend  over"
9080 PRINT"een maximaal tegoed van FL. 10000.00."
9090 PRINT"(over  het  meerdere  wordt  geen rente"
9100 PRINT"vergoed!)":PRINT
9110 PRINT"De periode waarover wordt gerekend, mag"
9120 PRINT"lopen vanaf 1 januari t/m  31  december"
9130 PRINT"van hetzelfde jaar."
9140 PRINT"Deze periode stelt U zelf in!"
9150 GOSUB3100:GOSUB2170:PRINT
9160 PRINT"U kunt inlezen:"
9170 PRINT" -bestanden van BANKREK/FINPLAN,"
9180 PRINT" -INTEGRALE bestanden bestaande uit een"
9190 PRINT"  samenvoeging    van   bestanden   uit"
9200 PRINT"  BANKREK/FINPLAN en CHEQUEBOEK."
9210 PRINT"  ('UTILITY NO.1')":PRINT
9220 PRINT"Ook  dit  programma  is een INFORMATIE-"
9230 PRINT"DOORVOER-PROGRAMMA.":PRINT
9240 PRINT"U moet zelf letten op de  volgorde  van"
9250 PRINT"inlezen van de bestanden als de periode"
9260 PRINT"waarover  moet  worden  gerekend,  niet"
9270 PRINT"wordt gedekt door EEN bestand!"
9280 PRINT"Het programma vraagt net zolang om  een"
9290 PRINT"bestand  tot  (t/m)  de  EIND-datum  is"
9300 PRINT"bereikt.":GOSUB3100:GOSUB2170:PRINT
9310 PRINT"Nadat  het eerste bestand is ingelezen,"
9320 PRINT"wordt    het    BEGIN-saldo     zonodig"
9330 PRINT"bijgesteld.":PRINT
9340 PRINT"U kunt naar keuze de berekening van het"
9350 PRINT"rentenummer volgen of niet.":PRINT
9360 PRINT"Toegepaste formule:":PRINT
9370 PRINT"         DGN   SALDO"
9380 PRINT"    RN = --- * -----"
9390 PRINT"          JD    100":PRINT
9400 PRINT"    JD = 365 of 366":PRINT
9410 PRINT"Het bijschrijven van  de  rente  in  uw"
9420 PRINT"'BANKREK/FP' moet U zelf doen!"
9430 GOSUB3100:GOSUB2170:PRINT
9440 L=39:GOSUB3050
9450 PRINT"Inlezen":GOSUB3050
9460 PRINT"20100-20500":GOSUB3050
9470 PRINT"INPUT N":PRINT"FORI=1 TO N"
9480 PRINT"INPUT N$(I)":PRINT"NEXT I":GOSUB3050
9490 PRINT:PRINT"Lengte van een post: MAX. 45 kar.":PRINT
9500 PRINT"Denk aan MAX. aantal posten!"
9510 PRINT" (Regel 25000 DATA ";NR;")"
9520 GOSUB3100:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$=N2$:VE=2:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T=VAL(RIGHT$(N$(N),1))
20620 IF(T=2) AND(T1=0) THENT1=2
20630 IF(T=4) AND(T1=0) THENT1=4
20640 IFT=2 THENN3$="Bankrek/Finplan":GOTO20700
20650 IFT=4 THENN3$="Integraal Bestand":GOTO20700
20660 GOSUB2150:FORK=1 TO3:GOSUB250:NEXTK
20670 HO=0:VE=12:GOSUB110
20680 PRINT"U hebt het verkeerde bestand ingelezen!"
20690 GOSUB3100:GOTO10000
20700 IFT1<>T THEN20750
20710 TD$=LEFT$(N$(N),6):TS$=MID$(N$(N),7,9)
20720 TE$=LEFT$(N$(1),6):L$=TE$:GOSUB2200:TE=SS
20730 TL$=LEFT$(N$(N-1),6):L$=TL$:GOSUB2200:TL=SS
20740 N$(Î ="":N=N-1:RETURN
20750 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
20760 HO=0:VE=8:GOSUB110
20770 PRINT"Steeds in DEZELFDE categorie blijven!"
20780 PRINT:PRINT"Bankrek/FP  --->  Bankrek/FP"
20790 PRINT:PRINT"Integr.Best.--->  Integr.Bestand"
20800 GOSUB250:VE=16:GOSUB110
20810 PRINT"Wilt U een ander bestand inlezen?"
20820 PRINT:PRINT" (J/N): ";
20830 GOSUB210:PRINTIN$:GOSUB23000
20840 IFIN$="J" THENGOSUB8800:GOTO20000
20850 GOTO10000
20990 RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA"1234"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM * RENTE-BEREKENING *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U 
30120 REM S  EIGEN ONTWERP   *
30130 REM   JAN.85/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM
