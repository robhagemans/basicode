1000 A=70:GOTO 20:REM Solitair-Dame
1010 DIM A(64)
1020 IF K=0 THEN GOSUB 30020
1030 GOSUB 18020
1040 GOSUB 19020
1050 VE=22:HO=3:GOSUB 110:PRINT "                        ";
1060 GOSUB 110
1070 PRINT "von";:INPUT B:VE=22:HO=11:GOSUB 110
1080 PRINT "nach";:INPUT C
1090 B=ABS(INT(B)):C=ABS(INT(C))
1100 IF (B=0) AND (C=0) THEN GOTO 1290
1110 IF (B<1) OR (C<1) OR (B>64) OR (C>64) THEN GOTO 1270
1120 IF (ABS(B-C)<>18) AND (ABS(B-C)<>14) THEN GOTO 1270
1130 IF A(B)=0 THEN GOTO 1270
1140 IF A(C)=1 THEN GOTO 1270
1150 IF A((B+C)/2)=0 THEN GOTO 1270
1160 D=B-INT((B+1)/8)*8:E=C-INT((C+1)/8)*8
1170 IF (D<3) AND (E<3) THEN GOTO 1270
1180 A(B)=0:A((B+C)/2)=0:A(C)=1:K=K-1
1190 HO=5*((B-1)-INT((B-1)/8)*8)+2:VE=2*INT((B-1)/8)+3
1200 GOSUB 110:PRINT " ";
1210 HO=5*((C-1)-INT((C-1)/8)*8)+2:VE=2*INT((C-1)/8)+3
1220 GOSUB 110:PRINT "*";
1230 HO=5*(((B+C)*0.5-1)-INT(((B+C)*0.5-1)/8)*8)+2
1240 VE=2*INT(((B+C)*0.5-1)/8)+3
1250 GOSUB 110:PRINT " ";
1260 HO=3:VE=20:GOSUB 110:PRINT "noch";K;"Steine ";:GOTO 1280
1270 GOSUB 250
1280 GOTO 1050
1290 GOSUB 100:PRINT "Es blieben";K;"Steine uebrig.":PRINT
1300 PRINT "Noch ein Spiel (j/n) ?":GOSUB 210
1310 IF (IN$<>"j") AND (IN$<>"J") THEN GOTO 950
1320 GOTO 1020
18000 REM #################################################
18010 REM Initialisierung fuer neues Spiel
18020 K=48
18030 FOR I=1 TO 64:A(I)=1:NEXT I
18040 FOR I=0 TO 3
18050 FOR J=0 TO 3
18060 A(19+J+I*8)=0
18070 NEXT J
18080 NEXT I
18090 RETURN
19000 REM #################################################
19010 REM Start-Spielfeld zeichnen
19020 GOSUB 100
19030 SR$="SOLITAIR - DAME":HO=9:VE=0:GOSUB 110:GOSUB 150
19040 SR$=" *1   *2   *3   *4   *5   *6   *7   *8"
19050 HO=1:VE=3:GOSUB 110:PRINT SR$;
19060 SR$=" *9  1*0  1*1  1*2  1*3  1*4  1*5  1*6" 
19070 HO=1:VE=5:GOSUB 110:PRINT SR$;
19080 SR$="1*7  1*8  1 9  2 0  2 1  2 2  2*3  2*4"
19090 HO=1:VE=7:GOSUB 110:PRINT SR$;
19100 SR$="2*5  2*6  2 7  2 8  2 9  3 0  3*1  3*2"
19110 HO=1:VE=9:GOSUB 110:PRINT SR$;
19120 SR$="3*3  3*4  3 5  3 6  3 7  3 8  3*9  4*0"
19130 HO=1:VE=11:GOSUB 110:PRINT SR$;
19140 SR$="4*1  4*2  4 3  4 4  4 5  4 6  4*7  4*8"
19150 HO=1:VE=13:GOSUB 110:PRINT SR$;
19160 SR$="4*9  5*0  5*1  5*2  5*3  5*4  5*5  5*6"
19170 HO=1:VE=15:GOSUB 110:PRINT SR$;
19180 SR$="5*7  5*8  5*9  6*0  6*1  6*2  6*3  6*4"
19190 HO=1:VE=17:GOSUB 110:PRINT SR$;
19200 HO=3:VE=20:GOSUB 110:PRINT "noch";k;"Steine ";
19210 RETURN
30000 REM #####################################################
30010 REM Spielregel bei erstem Start
30020 GOSUB 100
30030 sr$="SOLITAIR - DAME":ho=9:ve=0:GOSUB 110:GOSUB 150
30040 PRINT:PRINT
30050 PRINT " Das Spiel ist eine Kombination aus"
30060 PRINT "Solitair (Steckhalma) und Dame."
30070 PRINT:PRINT " Auf einem Schachbrett stehen 48 Steine"
30080 PRINT "am Rand, in der Mitte sind  4 * 4"
30090 PRINT "Felder frei."
30100 PRINT:PRINT " Wird mit einem Stein ein anderer"
30110 PRINT "diagonal uebersprungen, wird der"
30120 PRINT "uebersprungene Stein vom Spielfeld"
30130 PRINT "entfernt. Natuerlich ist es das Ziel,"
30140 PRINT "moeglichst wenig Steine uebrig zu"
30150 PRINT "lassen."
30160 PRINT:PRINT " Das Spiel wird durch Eingabe von"
30170 PRINT "zweimal 0 beendet."
30180 PRINT:PRINT:PRINT "  Viel Spass!":GOSUB 210
30190 RETURN
32000 REM #####################################################
32010 REM ##                                                 ##
32020 REM ##  Thomas Rademacher                              ##
32030 REM ##  Friedrich-Engels-Str. 44                       ##
32040 REM ##  D-99086 Erfurt                                 ##
32050 REM ##                                                 ##
32060 REM ##  Januar 2003                                    ##
32070 REM ##                                                 ##
32080 REM ##  erstellt auf emuliertem Amstrad PCW            ##
32090 REM ##  (PCWEMU v2.01 fuer WINDOWS von John Elliott)   ##
32100 REM ##                                                 ##
32110 REM ##  Spielidee: "Eine Dame" aus dem Buch            ##
32120 REM ##  "Basic Computer Spiele Mikrocomputer-Ausgabe"  ##
32130 REM ##  Band 1                                         ##
32140 REM ##  herausgegeben von David H. Ahl                 ##
32150 REM ##  SYBEX-Verlag GmbH, Duesseldorf                 ##
32160 REM ##                                                 ##
32170 REM #####################################################
