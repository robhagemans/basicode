1000 A=1000:GOTO 20:REM  RSA - TEIL 2
1010 DIM T$(2),TE$(16),LE(16),LT(16),CO(16,80),ZC(1200)
1020 KT$=""
1030 REM 
1040 U1$=" Texteingabe "
1050 U2$=" Codierung "
1060 U4$=" Anzeige / Ausdruck "
1070 U3$=" Speichern (WRITE) "
1080 REM 
1090 REM  --- SCHIRMGROESSE
1100 REM 
1110 H=HO:SP=HO-1:ZE=VE-1:ZG=INT (H/9)
1120 REM 
1130 REM  --- TITEL
1140 T$(0)="                         "
1150 T$(1)=" Die RSA - Codierung - 2 "
1160 T$(2)="                         "
1170 GOSUB 100
1180 FOR VE=0 TO 2
1190 L=LEN (T$(VE)):HO=INT ((H-6-L)/2):GOSUB 110
1200 SR$=T$(VE):GOSUB 150:PRINT 
1210 NEXT VE
1220 REM 
1230 REM  --- MENU
1240 REM 
1250 HO=2:VE=5:GOSUB 110
1260 SR$="E":GOSUB 150:PRINT "- Texteingabe"
1270 HO=2:VE=7:GOSUB 110
1280 SR$="C":GOSUB 150:PRINT "- Codierung"
1290 HO=2:VE=9:GOSUB 110
1300 SR$="A":GOSUB 150:PRINT "- Anzeige / Ausdruck"
1310 HO=2:VE=11:GOSUB 110
1320 SR$="S":GOSUB 150:PRINT "- Speichern (WRITE)"
1330 HO=2:VE=13:GOSUB 110
1340 SR$="X":GOSUB 150:PRINT "- Ende"
1350 HO=28:VE=13:GOSUB 110
1360 GOSUB 210
1370 IF IN=69 THEN GOSUB 1460:GOTO 1180
1380 IF IN=67 THEN GOSUB 1900:GOTO 1180
1390 IF IN=65 THEN GOSUB 3000:GOTO 1180
1400 IF IN=83 THEN GOSUB 4000:GOTO 1180
1410 IF IN=88 THEN GOTO 950
1420 GOSUB 250:GOTO 1360
1430 REM 
1440 REM  --- TEXTEINGABE
1450 REM 
1460 GOSUB 100
1470 L=LEN (U1$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
1480 SR$=U1$:GOSUB 150
1490 REM 
1500 HO=0:VE=2:GOSUB 110
1510 SR=H-10:GOSUB 300
1520 PRINT "Die zu codierende Nachricht kann max. 15"
1530 PRINT "Zeilen (";SR$;" Zeichen/Zeile) umfassen.":PRINT 
1540 PRINT "Zum Abschluss der Eingabe ist in einer"
1550 PRINT "Zeile nur '*' einzugeben.":PRINT 
1560 PRINT "Zur Texteingabe Taste druecken ! ";
1570 GOSUB 210:GOSUB 100
1580 L=LEN (U1$):HO=INT ((H-3-L)/2):VE=0:GOSUB 110
1590 SR$=U1$:GOSUB 150
1600 CT=2
1610 HO=9:VE=2:GOSUB 110
1620 FOR N=1 TO (H-10):PRINT "x";:NEXT N
1630 FOR J=1 TO 16
1640 HO=0:VE=22:GOSUB 110
1650 PRINT "                          "
1660 HO=0:VE=2+J:V=VE:GOSUB 110
1670 CN=0:SR=J:GOSUB 300:GOSUB 310
1680 IF J<16 THEN GOTO 1720
1690 HO=0:VE=V:GOSUB 110:PRINT "Ende ! ? *";
1700 HO=9:VE=V:GOSUB 110:GOSUB 210:IF IN<> 13 THEN GOTO 1690
1710 JE=J-1:GOTO 1830
1720 PRINT "Zl.";SR$;": ";:TE$(J)="":LT(J)=0:INPUT TE$(J)
1730 LT(J)=LEN (TE$(J))
1740 IF TE$(J)="*" THEN JE=J-1:J=16:GOTO 1830
1750 IF RIGHT$(TE$(J),1)<> " " THEN TE$(J)=TE$(J)+" "
1760 IF LT(J)>= (H-9) THEN GOTO 1820
1770 HO=0:VE=22:GOSUB 110
1780 SR$=" Eingabe o.k.? (j/n) ":GOSUB 150:GOSUB 210
1790 IF IN=74 THEN GOTO 1830
1800 IF IN=78 THEN GOTO 1820
1810 GOTO 1770
1820 HO=0:VE=V:GOSUB 110:FOR N=0 TO H:PRINT " ";:NEXT N:GOTO 1640
1830 NEXT J
1840 PRINT :PRINT "Eingabe beendet !"
1850 PRINT :PRINT "Klartext im RAM !"
1860 GOSUB 5050:RETURN 
1870 REM 
1880 REM  --- CODIERUNG
1890 REM 
1900 GOSUB 100:GOSUB 5140
1910 IF JE=0 THEN GOTO 2570
1920 GOSUB 100:GOSUB 5140
1930 HO=0:VE=2:GOSUB 110
1940 PRINT "Eingabe: Modul und Codierschluessel":PRINT 
1950 PRINT "Tastatur oder File      <T/F> ";
1960 GOSUB 210
1970 IF IN=70 THEN GOTO 2000
1980 IF IN=84 THEN GOTO 2210
1990 GOTO 1930
2000 HO=0:VE=6:GOSUB 110
2010 INPUT "Name des Schluessel-Files: ";NF$:PRINT 
2020 PRINT "Cassette (BASICODE) oder      "
2030 PRINT "Diskette                <C/D> ";
2040 GOSUB 210
2050 IF IN=68 THEN NF=4:GOTO 2080
2060 IF IN=67 THEN NF=0:GOTO 2080
2070 GOTO 2040
2080 GOSUB 500
2090 GOSUB 540:M=VAL (IN$):GOSUB 5090
2100 GOSUB 540:PM=VAL (IN$):GOSUB 5090:PM=0
2110 GOSUB 540:C=VAL (IN$):GOSUB 5090
2120 GOSUB 540:D=VAL (IN$):GOSUB 5090:D=0
2130 GOSUB 580
2140 HO=0:VE=11:GOSUB 110
2150 PRINT "Schluesselzahlen geladen; der"
2160 PRINT "oeffentliche Schluessel lautet:":PRINT 
2170 PRINT "           Modul (M) =";M:PRINT 
2180 PRINT "Codierschluessel (C) =";C
2190 GOSUB 5050:GOTO 2280
2200 REM 
2210 GOSUB 100:GOSUB 5140
2220 HO=0:VE=2:GOSUB 110
2230 PRINT "Eingabe:";
2240 INPUT "   Modul M ";M:PRINT 
2250 INPUT "Codierschluessel C ";C
2260 GOSUB 5050
2270 REM 
2280 GOSUB 100:GOSUB 5140
2290 HO=0:VE=2:GOSUB 110
2300 PRINT "Der Text lautet verschluesselt:":PRINT 
2310 FL=0:Z=0:J=0:K=0:NN=1
2320 FOR J=1 TO JE
2330 LE(J)=LEN (TE$(J))
2340 FOR K=1 TO LE(J)
2350 N=ASC(MID$(TE$(J),K,1))
2360 IF (N>= 193) AND (N<= 218) THEN N=N-128:GOTO 2380
2370 IF (N>= 65) AND (N<= 90) THEN N=N+32
2380 X=N:Y=C:CO=1
2390 IF Y=2*INT (Y/2) THEN GOTO 2550
2400 Y=Y-1:CO=CO*X:CO=CO-INT (CO/M)*M
2410 IF Y<> 0 THEN GOTO 2390
2420 PRINT CO;:Z=Z+1
2430 IF Z=ZG THEN PRINT :PRINT :Z=0
2440 CO(J,K)=CO:ZC(NN)=CO:NN=NN+1
2450 GOSUB 120
2460 IF VE>= 20 THEN GOSUB 250:GOSUB 5050:GOSUB 5180
2470 NEXT K:NEXT J
2480 REM 
2490 EJ=J:EK=K:EZ=NN:FL=1
2500 REM 
2510 GOSUB 120:VE=VE+2:HO=0:GOSUB 110
2520 PRINT "Codierung beendet !":GOSUB 250
2530 GOSUB 5050:RETURN 
2540 REM 
2550 Y=Y/2:X=X*X:X=X-INT (X/M)*M:GOTO 2410
2560 REM 
2570 HO=0:VE=2:GOSUB 110
2580 SR$=" Kein Text gespeichert ! ":GOSUB 150
2590 HO=2:VE=4:GOSUB 110
2600 SR$="E":GOSUB 150:PRINT "- Eingabe"
2610 HO=2:VE=6:GOSUB 110
2620 SR$="L":GOSUB 150:PRINT "- Laden (READ)"
2630 HO=2:VE=8:GOSUB 110
2640 SR$="M":GOSUB 150:PRINT "- Menu       ";
2650 GOSUB 210
2660 IF IN=69 THEN GOTO 1460
2670 IF IN=76 THEN GOTO 2700
2680 IF IN=77 THEN GOSUB 100:RETURN 
2690 GOTO 2630
2700 HO=2:VE=10:GOSUB 110
2710 PRINT "Text einlesen von"
2720 HO=2:VE=11:GOSUB 110
2730 PRINT "Cassette / Disk   <C/D> ";:GOSUB 210
2740 IF IN=68 THEN NF=4:GOTO 2770
2750 IF IN=67 THEN NF=0:GOTO 2770
2760 GOTO 2700
2770 HO=2:VE=13:GOSUB 110
2780 INPUT "Textfile - Name ";NF$
2790 IF RIGHT$(NF$,2)<> "-c" THEN GOTO 2820
2800 PRINT :PRINT "  Schon codierter Text !"
2810 HO=19:VE=13:GOSUB 110:PRINT "       ":GOTO 2770
2820 KT$=NF$:GOSUB 500
2830 GOSUB 540:JE=VAL (IN$)
2840 IF IN=-1 THEN GOTO 5090
2850 FOR J=1 TO JE
2860 GOSUB 540:TE$(J)=IN$
2870 IF IN=-1 THEN GOTO 5090
2880 NEXT J
2890 GOSUB 580:IF IN=-1 THEN GOTO 5090
2900 HO=2:VE=15:GOSUB 110
2910 PRINT "Klartext-File gelesen ! "
2920 HO=2:VE=17:GOSUB 110
2930 PRINT "Anzeige / Codieren <A/C> ";:GOSUB 210
2940 IF IN=67 THEN GOTO 1910
2950 IF IN=65 THEN GOSUB 3000:RETURN 
2960 GOTO 2920
2970 REM 
2980 REM  --- ANZEIGE/AUSDRUCK
2990 REM 
3000 GOSUB 100
3010 L=LEN (U4$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
3020 SR$=U4$:GOSUB 150
3030 REM 
3040 HO=0:VE=2:GOSUB 110
3050 PRINT "Anzeige auf Bildschirm"
3060 PRINT "          oder Drucker   <B/D> ";:GOSUB 210
3070 IF (IN<> 66) AND (IN<> 68) THEN GOTO 3040
3080 W=IN:IF W=66 THEN GOTO 3120
3090 HO=0:VE=5:GOSUB 110
3100 PRINT "Drucker betriebsbereit ? <J/N> ";:GOSUB 210
3110 IF (IN<> 74) AND (IN<> 78) THEN GOTO 3090
3120 PP=0:IF IN=74 THEN PP=1
3130 SR$=CHR$ (W):GOSUB 330:HO=29:VE=3:GOSUB 110:GOSUB 150
3140 REM 
3150 HO=0:VE=7:GOSUB 110
3160 SR$="K":GOSUB 150:PRINT "- Klartext"
3170 HO=0:VE=9:GOSUB 110
3180 SR$="V":GOSUB 150:PRINT "- Verschluesselter Text ";
3190 GOSUB 210
3200 IF IN=75 THEN GOTO 3240
3210 IF IN=86 THEN GOTO 3590
3220 GOTO 3170
3230 REM 
3240 IF JE<> 0 THEN GOTO 3300
3250 HO=0:VE=11:GOSUB 110
3260 PRINT "  Kein Klartext im RAM !":PRINT 
3270 PRINT "  Zurueck zum Menu !"
3280 GOSUB 5050:GOSUB 100:RETURN 
3290 REM 
3300 IF PP=1 THEN GOTO 3480
3310 PRINT :PRINT 
3320 PRINT "K l a r t e x t: ";KT$
3330 PRINT "----------------------"
3340 FOR J=1 TO JE
3350 LE=LEN (TE$(J)):IF LE>H THEN GOTO 3380
3360 PRINT TE$(J):GOTO 3450
3370 REM 
3380 FOR M=1 TO H
3390 IF MID$(TE$(J),M,1)=" " THEN S=M
3400 IF S>H THEN M=H
3410 NEXT M
3420 TL$=LEFT$(TE$(J),S):TR$=RIGHT$(TE$(J),LE-S)
3430 PRINT TL$
3440 PRINT TR$
3450 NEXT J
3460 GOSUB 5050:GOSUB 100:RETURN 
3470 REM 
3480 GOSUB 360
3490 SR$="K l a r t e x t: "+KT$
3500 GOSUB 350:GOSUB 360
3510 SR$="----------------------"
3520 GOSUB 350:GOSUB 360
3530 FOR J=1 TO JE
3540 SR$=TE$(J):GOSUB 350:GOSUB 360
3550 NEXT J
3560 GOSUB 360:GOSUB 360
3570 GOSUB 5050:GOSUB 100:RETURN 
3580 REM 
3590 IF (J<> 0) OR (K<> 0) THEN GOTO 3650
3600 HO=0:VE=11:GOSUB 110
3610 PRINT "  Kein verschluesselter Text":PRINT 
3620 PRINT "  im RAM - Zurueck zum Menu !"
3630 GOSUB 5050:GOSUB 100:RETURN 
3640 REM 
3650 IF PP=1 THEN GOTO 3810
3660 HO=0:VE=11:GOSUB 110
3670 PRINT "Verschluesselter Text: ";KT$;"-c"
3680 PRINT "------------------------------"
3690 Z=0
3700 FOR NN=1 TO EZ
3710 IF ZC(NN)=0 THEN GOTO 3780
3720 SR=ZC(NN):GOSUB 300
3730 S$="":FOR S=1 TO 8-LEN (SR$):S$=S$+" ":NEXT S
3740 PRINT S$+LEFT$(SR$,LEN (SR$));:Z=Z+1
3750 IF Z=ZG THEN PRINT :PRINT :Z=0
3760 GOSUB 120
3770 IF VE>= 20 THEN GOSUB 250:GOSUB 5050:GOSUB 5260
3780 NEXT NN
3790 GOSUB 5050:GOSUB 100:RETURN 
3800 REM 
3810 GOSUB 360
3820 SR$="Verschluesselter Text: "+KT$+"-c"
3830 GOSUB 350:GOSUB 360
3840 SR$="------------------------------"
3850 GOSUB 350:GOSUB 360
3860 Z=0
3870 FOR NN=1 TO EZ
3880 IF ZC(NN)=0 THEN GOTO 3940
3890 SR=ZC(NN):GOSUB 300
3900 S$="":FOR S=1 TO 8-LEN (SR$):S$=S$+" ":NEXT S
3910 SR$=S$+LEFT$(SR$,LEN (SR$)):Z=Z+1:GOSUB 350
3920 IF Z=8 THEN GOSUB 360:GOSUB 360:Z=0
3930 GOSUB 120
3940 NEXT NN
3950 GOSUB 360:GOSUB 360
3960 GOSUB 5050:GOSUB 100:RETURN 
3970 REM 
3980 REM  --- SPEICHERN
3990 REM 
4000 GOSUB 100
4010 L=LEN (U3$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
4020 SR$=U3$:GOSUB 150
4030 REM 
4040 HO=0:VE=2:GOSUB 110
4050 PRINT "Speichern:"
4060 HO=0:VE=4:GOSUB 110
4070 SR$="K":GOSUB 150:PRINT "- Klartext"
4080 HO=0:VE=6:GOSUB 110
4090 SR$="V":GOSUB 150:PRINT "- Verschluesselter Text"
4100 HO=32:VE=6:GOSUB 110
4110 GOSUB 210
4120 IF IN=75 THEN GOTO 4160
4130 IF IN=86 THEN GOTO 4540
4140 GOTO 4100
4150 REM 
4160 IF JE<> 0 THEN GOTO 4210
4170 HO=0:VE=8:GOSUB 110
4180 PRINT "Kein Klartext im RAM !"
4190 GOSUB 5050:RETURN 
4200 REM 
4210 HO=0:VE=2:GOSUB 110
4220 PRINT "Speichern des Klartextes:"
4230 HO=2:VE=4:GOSUB 110
4240 SR$="D":GOSUB 150:PRINT "- Diskette"
4250 HO=2:VE=6:GOSUB 110
4260 SR$="C":GOSUB 150:PRINT "- Cassette (BASICODE)"
4270 HO=2:VE=8:GOSUB 110
4280 SR$="N":GOSUB 150:PRINT "- NEIN"
4290 HO=29:VE=8:GOSUB 110:GOSUB 210
4300 IF IN=68 THEN NF=5:GOTO 4350
4310 IF IN=67 THEN NF=1:GOTO 4350
4320 IF IN=78 THEN GOSUB 100:RETURN 
4330 GOSUB 250:GOTO 4210
4340 REM 
4350 HO=0:VE=10:GOSUB 110
4360 NF$="":INPUT "Name des Files (max. 5 Zch.) ";NF$
4370 KT$=NF$:IF LEN (NF$)>5 THEN GOTO 4350
4380 GOSUB 500:GOSUB 5090
4390 SR=JE:GOSUB 300:GOSUB 560:GOSUB 5090
4400 REM 
4410 FOR J=1 TO JE
4420 SR$=TE$(J):GOSUB 560:GOSUB 5090
4430 NEXT J
4440 GOSUB 580:GOSUB 5090
4450 HO=0:VE=12:GOSUB 110
4460 PRINT "Klartext auf ";
4470 IF NF=5 THEN PRINT "Diskette "
4480 IF NF=1 THEN PRINT "Cassette (BASICODE) "
4490 PRINT "gespeichert.":PRINT 
4500 PRINT "Name:";:SR$=NF$:GOSUB 150
4510 REM 
4520 GOSUB 5050:RETURN 
4530 REM 
4540 GOSUB 100
4550 L=LEN (U3$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
4560 SR$=U3$:GOSUB 150
4570 REM 
4580 IF (J<> 0) AND (K<> 0) THEN GOTO 4640
4590 HO=0:VE=2:GOSUB 110
4600 PRINT " Kein verschluesselter "
4610 PRINT " Text im RAM !"
4620 GOSUB 5050:RETURN 
4630 REM 
4640 HO=0:VE=2:GOSUB 110
4650 PRINT "Speichern des verschluess. Textes:"
4660 HO=2:VE=4:GOSUB 110
4670 SR$="D":GOSUB 150:PRINT "- Diskette"
4680 HO=2:VE=6:GOSUB 110
4690 SR$="C":GOSUB 150:PRINT "- Cassette (BASICODE)"
4700 HO=2:VE=8:GOSUB 110
4710 SR$="N":GOSUB 150:PRINT "- NEIN"
4720 HO=29:VE=8:GOSUB 110:GOSUB 210
4730 IF IN=68 THEN NF=5:GOTO 4780
4740 IF IN=67 THEN NF=1:GOTO 4780
4750 IF IN=78 THEN GOSUB 100:RETURN 
4760 GOSUB 250:GOTO 4720
4770 REM 
4780 IF KT$<> "" THEN NF$=KT$+"-c":GOTO 4830
4790 HO=0:VE=10:GOSUB 110
4800 NF$="":INPUT "Name des Files (max. 5 Zch.) ";NF$
4810 IF LEN (NF$)>5 THEN GOTO 4790
4820 NF$=NF$+"-c"
4830 GOSUB 500:GOSUB 5090
4840 SR=EZ:GOSUB 300:GOSUB 560:GOSUB 5090
4850 REM 
4860 FOR NN=1 TO EZ
4870 SR=ZC(NN):GOSUB 300:GOSUB 560:GOSUB 5090
4880 NEXT NN
4890 REM 
4900 REM 
4910 GOSUB 580:GOSUB 5090
4920 GOSUB 250
4930 HO=0:VE=12:GOSUB 110
4940 PRINT "Codierter Text auf ";
4950 IF NF=5 THEN PRINT "Diskette "
4960 IF NF=1 THEN PRINT "Cassette (BASICODE) "
4970 PRINT "gespeichert.":PRINT 
4980 PRINT "Name:";:SR$=NF$:GOSUB 150
4990 REM 
5000 GOSUB 5050:RETURN 
5010 REM 
5020 REM  --- SUBROUTINEN
5030 REM 
5040 REM  ------------------------------------------------
5050 HO=0:VE=24:GOSUB 110
5060 SR$="Taste druecken =>":GOSUB 150
5070 GOSUB 210:GOSUB 100:RETURN 
5080 REM  ------------------------------------------------
5090 IF (IN=0) OR (IN=1) THEN RETURN 
5100 HO=0:VE=22:GOSUB 110
5110 SR$=" Schreib- / Lesefehler ! ":GOSUB 150
5120 GOSUB 210:RETURN 
5130 REM  ------------------------------------------------
5140 L=LEN (U2$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
5150 SR$=U2$:GOSUB 150:RETURN 
5160 HO=0:VE=2:GOSUB 110:RETURN 
5170 REM  ------------------------------------------------
5180 GOSUB 100:GOSUB 5140
5190 HO=0:VE=2:GOSUB 110
5200 PRINT "Fortsetzung:":PRINT :RETURN 
5210 REM  ------------------------------------------------
5220 L=LEN (U4$):HO=INT ((H-6-L)/2):VE=0:GOSUB 110
5230 SR$=U4$:GOSUB 150:RETURN 
5240 HO=0:VE=2:GOSUB 110:RETURN 
5250 REM  ------------------------------------------------
5260 GOSUB 100:GOSUB 5220
5270 HO=0:VE=2:GOSUB 110
5280 PRINT "Fortsetzung:":PRINT :RETURN 
5290 REM 
30000 REM  ------------------------------------------------
30010 REM  GRUNDLAGE DIESES PROGRAMMS UND DER TEILE 1 UND 3
30020 REM  IST FOLGENDER BEITRAG:
30030 REM 
30040 REM    MARCEL SUTTER
30050 REM    EIN STREIFZUG DURCH DIE KRYPTOLOGIE
30060 REM    M+K COMPUTER, LUZERN, 1990, HEFT 4
30070 REM 
32000 REM  ---------------------------------------
32010 REM  FRIEDRICH DORMEIER
32020 REM  BISMARCKSTR. 11A
32030 REM  D-1000 BERLIN 39
32040 REM  (030) 803 31 11
32050 REM 
32060 REM  C-128 - 9/1991
32070 REM  ---------------------------------------
32080 REM  Erstausstrahlung: DS-Kultur
32090 REM -spezial 911016
