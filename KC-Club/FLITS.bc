1000 A=500:GOTO20:REM ** FLITS ** / ** BLITZ **
1010 DIML$(8):AA=ASC("A"):SP=0:SV=0
1020 T$=" "
1030 GOSUB100
1040 PRINT:PRINT
1050 SR$=" R E A K T I O N":GOSUB150:PRINT:PRINT
1060 PRINT"Das Programm laesst kurz eine Zahl,"
1070 PRINT"ein Wort oder eine Autonummer auf"
1080 PRINT"dem Bildschirm erscheinen. Das Er-"
1090 PRINT"schienene muss danach genau eingeben"
1100 PRINT"werden. Nur wenn das richtig gemacht"
1110 PRINT"wurde, wird die folgende Anzeigezeit "
1120 PRINT"kuerzer. Wenn es nicht richtig war,"
1130 PRINT"dauert es beim naechsten mal etwas "
1140 PRINT"laenger. Unter dem Ausgabefeld steht"
1150 PRINT"die Ausgabezeit."
1160 PRINT"Mach sie so klein wie moeglich!"
1170 PRINT:PRINT:PRINT"Eine Taste --->";:GOSUB210:GOSUB100
1180 PRINT:PRINT:GP=0.0001:SD=20:REM >> FLITSDUUR <<
1190 SR$=" W A E H L E ":GOSUB150:PRINT:PRINT
1200 PRINT"( S = Stoppen )":PRINT:PRINT
1210 PRINT"Willst du Zahlen ":PRINT"          Woerter"
1220 PRINT"          Autonummern ?":PRINT
1230 PRINT"Druecke Z, W oder A";:GOSUB210
1240 IFIN=83 THEN950
1250 IF(IN<>65)AND(IN<>90)AND(IN<>87)THENPRINT:GOTO1230
1260 PRINT:PRINT:K1=1:IFIN=87 THENK1=2
1270 IFIN=65 THENK1=3:K2=8
1280 ONK1 GOTO1290,1300,2000
1290 PRINT"Zahlen mit wieviel Ziffern?":GOTO1310
1300 PRINT"Woerter mit wieviel Buchstaben?"
1310 PRINT"Druecke 1, 2, 3, 4 oder 5 ";:GOSUB210
1320 IFIN=83 THEN950
1330 IF(IN<49)OR(IN>53)THENPRINT:GOTO1310
1340 K2=IN-48
2000 GOSUB600:PH=1/HG:PV=1/VG
2010 REM ---TREK LIJNEN---
2020 CN=0:HO=.15:VE=.3:GOSUB620:HO=.38:GOSUB630
2030 HO=.42+K2*9*PH:VE=.3:GOSUB620:HO=.85:GOSUB630
2040 HO=0:VE=.2:GOSUB620:HO=1-PH:GOSUB630
2050 VE=.4:GOSUB630:HO=0:GOSUB630:VE=.2:GOSUB630
2060 HO=.3:VE=.1:SR$="! = MENU":GOSUB650
3000 REM ===HOOFDPROGRAMMA===
3010 ONK1 GOTO3030,3060,3250
3020 REM ---GETALLENGENERATOR---
3030 GOSUB260:SR=10000+INT(RV*90000)
3040 GOSUB300:X$=LEFT$(SR$,K2):SR=VAL(X$):GOTO3510
3050 REM ---WOORDENGENERATOR---
3060 ONK2 GOTO3080,3080,3120,3150,3200
3070 REM ---1 LETTER---
3080 GOSUB260:X=AA+INT(RV*26):X$=CHR$(X):IFK2=1 THEN3510
3090 REM ---2 LETTERS---
3100 GOSUB260:X=AA+INT(RV*26):X$=X$+CHR$(X):GOTO3510
3110 REM ---3 LETTERS---
3120 RESTORE:GOSUB260:FORN=1 TORV*50:READX$:NEXTN
3130 GOTO3510
3140 REM ---4 LETTERS---
3150 RESTORE
3160 READX$:IFX$<>"W4" THEN3160
3170 GOSUB260:FORN=1 TORV*50:READX$:NEXTN
3180 GOTO3510
3190 REM ---5 LETTERS---
3200 RESTORE
3210 READX$:IFX$<>"W5" THEN3210
3220 GOSUB260:FORN=1 TORV*50:READX$:NEXTN
3230 GOTO3510
3240 REM ---AUTONUMMERGENERATOR---
3250 FORN=1 TO4:GOSUB260:L$(N)=CHR$(AA+RV*26):NEXTN
3260 FORN=5 TO8
3270 GOSUB260:SR=INT(RV*10):GOSUB300:L$(N)=SR$:NEXTN
3280 X$=L$(1)+L$(2)+"-"+L$(5)+L$(6)+"-"+L$(3)+L$(4)
3290 REM 1-4=LETTERS, 5-8=CIJFERS, HIER DUS LL-CC-LL
3500 REM --- WIS VORIGE FLITSDUUR ---
3510 CN=1:HO=0:VE=.8:SR$=T$:GOSUB650:CN=0
3520 REM ---PRINT DE FLITSDUUR---
3530 SR=SD/10:CT=3:CN=1:GOSUB310
3540 CN=0:SR$=SR$+" sek.":GOSUB650:T$=SR$
3550 REM ---WERK GRAFIEK BIJ---
3560 GP=GP+PH:IFGP>=1-PH THEN950
3570 REM ---DOE DE FLITS---
3580 Z$="":FORN=1 TOLEN(X$):Q$=MID$(X$,N,1)
3590 IFQ$="0" THENQ$="O"
3600 Z$=Z$+Q$:NEXTN:REM ---0 WORDT O---
3610 HO=.4:VE=.29:SR$=Z$:GOSUB650
3620 GOSUB400:REM >> FLITSDUUR <<
3630 CN=1:GOSUB650:CN=0
3640 HO=.37:VE=.6:SR$="?":GOSUB650
3650 REM ---INPUT ANTWOORD---
3660 Y$="":LY=0:HO=.4:VE=.6
3670 GOSUB210:IFIN=13 THEN3780
3680 IFIN$="!" THEN GOSUB100:GOTO1180
3690 IF(IN<33)OR(IN>127)THEN3670
3700 IF(IN=127)AND(LY=0)THEN3670
3710 IFIN<>127 THEN3760
3720 CN=1:SR$=Y$:GOSUB650:CN=0
3730 IFLEN(Y$)=1 THENY$="":GOTO3750
3740 Y$=LEFT$(Y$,LEN(Y$)-1)
3750 LY=LY-1:SR$=Y$:GOSUB650:GOTO3670
3760 Y$=Y$+IN$:LY=LY+1:SR$=Y$:GOSUB650:GOTO3670
3770 REM ---BEOORDELING---
3780 G=0:SD=SD+1:IFX$=Y$ THENG=1:SD=SD-2:IFSD=0 THENSD=1
3790 IFSD>40 THENSD=40
3800 HO=.4:VE=.6:SR$=Y$:GOSUB650
3810 IFG=1 THENHO=.4:VE=.7:SR$="RICHTIG !":GOSUB650:GOTO3830
3820 HO=0:VE=.7:SR$="SCHADE, es war "+X$:GOSUB650
3830 HO=.4:VE=.8:SR$="  << SPACE >> ":GOSUB650
3840 IFCN=1 THEN3880
3850 GOSUB210:IF(IN<>32)AND(IN$<>"!")THEN3850
3860 IFIN$="!" THEN GOSUB100:GOTO1180
3870 CN=1:GOTO3800
3880 CN=0:ONK1 GOTO3030,3060,3250
3890 REM
25000 DATA"DAK","HOK","MAN","SAP","VIS"
25010 DATA"LAK","MIS","UIT","BOS","TAK"
25020 DATA"KAT","UIL","BOK","TOR","RAT"
25030 DATA"VAT","VET","PAN","WAS","ZOT"
25040 DATA"TIK","REM","POP","KAR","BUS"
25050 DATA"LEK","HIJ","WIJ","WEI","MEI"
25060 DATA"KOU","KOM","DUS","KAM","LAM"
25070 DATA"DAS","SOK","ELF","ZES","MES"
25080 DATA"KEI","EIK","IEP","GEK","DUN"
25090 DATA"BOL","MUG","SIP","KOE","OEF"
25100 DATA"W4"
25110 DATA"HALT","HOUT","JONG","MUIS","PLOF"
25120 DATA"HOOP","KIST","PLAK","BALK","KURK"
25130 DATA"HARK","BANG","SLAK","GOED","MOOI"
25140 DATA"HOOI","OLIE","OPEN","LAAT","VLUG"
25150 DATA"KOEK","WESP","VIER","VUUR","HAND"
25160 DATA"BOEK","TIJD","ZWAK","MIER","DOEK"
25170 DATA"BIJL","ZEIL","STER","REUS","NEUS"
25180 DATA"KNIE","FLES","TAAL","TAAK","KAAL"
25190 DATA"RAND","WENS","SNEL","TOUW","HIER"
25200 DATA"ZALM","BEER","KRUK","VOUW","DEUR"
25210 DATA"W5"
25220 DATA"NEGEN","LEVER","BEEST","KATER","STANG"
25230 DATA"VLIEG","KEVER","METER","SLANG","GROND"
25240 DATA"BOTER","ANKER","LEPEL","BETER","PLONS"
25250 DATA"RATEL","LATER","ZEKER","KOPER","REGEL"
25260 DATA"MOTOR","KAPOT","STUUR","WAGON","KRUIK"
25270 DATA"KRAAN","WATER","GROEN","VADER","BROER"
25280 DATA"TANTE","GROEP","GELUK","JARIG","TAART"
25290 DATA"LENTE","BLOEM","TAFEL","STRIK","BOTER"
25300 DATA"BOVEN","KRIJT","BLAUW","HOPEN","KAART"
25310 DATA"LUCHT","KRAAI","LOKET","TYPEN","KLAAR"
25320 REM
32000 REM GEMAAKT OP BBC-COMPUTER
32010 REM JANUARI 1989
32020 REM CH.W.BREDERODE
32030 REM VON BONNINGHAUSENLAAN 33
32040 REM 2161 ES  LISSE
32050 REM
32060 REM Uebersetzt von
32070 REM Ronald Knak
32080 REM Dr.-H.-Wolf-Str.1
32090 REM Schwerin
32100 REM 2 7 5 8
32110 REM-------------------------------------------
32120 REM Erstausstrahlung: Deutschlandsender Kultur
32130 REM-spezial 910626
