1000 A=200:GOTO 20:REM ## ENERGIE-KOSTEN ##
1010 SR$="ELEKTRO-ENERGIEKOSTEN-BERECHNUNG"
1020 GOTO 3000
1030 HO=1:VE=2:GOSUB 110:GOSUB 150
1040 HO=0:VE=4:GOSUB 110
1050 PRINT" Eingabe der Leistungsparameter"
1060 PRINT"--------------------------------"
1070 PRINT
1200 INPUT" Arbeitspreis (DM/kWh) =";AP
1210 INPUT" fester Leistungspreis (DM) =";FL
1220 INPUT" variabl.Leistungspreis (DM/kWh) =";VL
1230 INPUT" Verrechnungspr. f.Zaehler (DM) =";VP
1240 PRINT" Durchschnitts-"
1250 INPUT" Hoechstpreis (DM/kWh) =";DH
1260 INPUT" Mindestpreis (DM/kWh) =";DM:PRINT
1270 INPUT" Umsatzsteuer (Mwst)(%) =";US:PRINT
1280 INPUT" Energie-Verbrauch (kWh/Jahr) =";JV
2000 REM
2010 JK=JV*(AP+VL)
2020 GK=JK+FL
2030 DK=GK/JV:C=US*.01+1
2040 IF DK>DH THEN 2400
2050 IF DK<DM THEN 2600
2060 GS=(GK+VP)*C
2200 PRINT
2210 PRINT" Gesamt-Kosten = ";INT(GS*100+.5)/100;
2220 PRINT" DM":PRINT
2230 PRINT" Durchschnittspreis/kWh =";
2240 PRINT INT(GK*C*1000/JV+.5)/1000;" DM"
2250 PRINT" (inkl.Mwst.)"
2300 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2310 GOTO 3000
2400 GS=DH*JV*C+VP:PRINT
2410 PRINT" Gesamt-Kosten = ";INT(GS*100+.5)/100;
2420 PRINT" DM":PRINT
2430 PRINT" Durchschnittspreis/kWh =";
2440 PRINT DH*C;" DM":PRINT
2450 PRINT"(inkl.Mwst.)"
2460 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2470 GOTO 3000
2600 GS=DM*JV*C+VP:PRINT
2610 PRINT" Gesamt-Kosten = ";INT(GS*100+.5)/100;
2620 PRINT" DM":PRINT
2630 PRINT" Durchschnittspreis/kWh =";
2640 PRINT DM*C;" DM"
2650 PRINT"(inkl.Mwst.)"
2660 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2680 GOTO 3000
3000 GOSUB 100:HO=1:VE=2:GOSUB 110:GOSUB 150
3010 HO=0:VE=4:GOSUB 110
3020 PRINT" M E N U E"
3030 PRINT" ---------"
3040 PRINT" -1- Erklaerung und Beispiel-Parameter"
3050 PRINT:PRINT" -2- Eingabe der Parameter"
3060 PRINT:PRINT" -3- Neuberechnung mit neuen"
3070 PRINT"     Energie-Verbrauch (kWh/Jahr)":PRINT
3080 PRINT" -4- Graphische Darstellung":PRINT
3090 PRINT" -5- Berechnung beenden"
3100 GOSUB 210
3110 IF IN$="1" THEN GOSUB 100:GOTO 6010
3120 IF IN$="2" THEN PRINT:GOSUB 100:GOTO 1030
3130 IF IN$="3" THEN GOSUB 100:GOTO 1280
3140 IF IN$="4" THEN 4010
3150 IF IN$="5" THEN 950
3160 GOTO 3000
4000 REM
4010 GOSUB 100
4020 PRINT"Eingabe des max.Energie-Verbrauchs "
4030 PRINT"(kWh/Jahr)":PRINT
4040 INPUT N
4050 INPUT"Eingabe der max.Energiekosten (DM)";MK
4100 GOSUB 600:CN=0
4110 HO=0:VE=.99:GOSUB 620
4120 HO=.99:VE=.99:GOSUB 630
4130 HO=0:VE=0:GOSUB 620
4140 HO=0:VE=.99:GOSUB 630
4150 FOR F=1 TO 3
4160 FOR E=0 TO 1 STEP .05
4170 HO=F/300:VE=E:GOSUB 620
4180 NEXT E
4190 NEXT F
4200 FOR F=1 TO 4
4210 FOR E=0 TO 1 STEP .05
4220 HO=E:VE=.99-F/200:GOSUB 620
4230 NEXT E
4240 NEXT F
4300 REM
4310 FOR M=0.01 TO N-100 STEP N/300
4320 JK=M*(AP+VL):GK=JK+FL:DK=GK/M:C=US*.01+1
4330 IF DK>DH THEN 4600
4340 IF DK<DM THEN 4800
4350 GS=(GK+VP)*C:DP=GK*C/M
4400 HO=M/N:VE=1-GS/MK:GOSUB 620
4410 VE=1-DP:GOSUB 620
4420 GOTO 5000
4600 GS=DH*M*C+VP:DP=DH*C
4610 HO=M/N:VE=1-GS/MK:GOSUB 620
4620 VE=1-DP:GOSUB 620
4700 GOTO 5000
4800 GS=DM*M*C+VP:DP=DM*C
4810 HO=M/N:VE=1-GS/MK:GOSUB 620
4820 VE=1-DP:GOSUB 620
4900 GOTO 5000
5000 NEXT M:HO=0:VE=0
5010 SR=MK:GOSUB 300:SR$=SR$+" DM (1.-DM/kWh)"
5020 GOSUB 650
5030 HO=.8:VE=.93:SR=N:GOSUB 300:SR$=SR$+" kWh"
5040 GOSUB 650
5050 HO=.3:VE=.55:SR$="(Durchschnittspreis/kWh)"
5060 GOSUB 650
5070 HO=.45:VE=.25:SR$="Energiekosten/Jahr"
5080 GOSUB 650
5090 SR$=" ELEKTRO-ENERGIEKOSTEN-BERECHNUNG"
5100 GOSUB 210:GOTO 3000
6000 REM ERKLAERUNG
6010 HO=4:VE=2:GOSUB 110:SR$="ERKLAERUNG":GOSUB 150
6020 HO=0:VE=4:GOSUB 110
6030 PRINT"In den 5 neuen Bundeslaendern hat die"
6040 PRINT"Einfuehrung einer neuen Energiekosten-"
6050 PRINT"Berechnung etwas Verwirrung gestiftet."
6060 PRINT
6070 PRINT"Dieses Programm ermoeglicht die"
6080 PRINT"Energiekosten-Berechnung fuer beliebige"
6090 PRINT"Tarifarten.":PRINT
6100 PRINT"Ein Beispiel mit graph. Darstellung"
6110 PRINT"fuer einen mittleren Haushalt im"
6120 PRINT"Tarifgebiet der ESAG (Sachsen)"
6130 PRINT"traegt sicherlich zum Verstaendnis bei"
6140 HO=30:VE=24:GOSUB 110:PRINT"TASTE=>":GOSUB 210
6150 GOSUB 100:PRINT" Beispiel:"
6160 PRINT" --------"
6170 PRINT" Arbeitspreis/kWh = 0.14 DM/kWh":PRINT
6180 PRINT" fester Leistungspreis = 89.40 DM":PRINT
6190 PRINT" variabl.Leistungspreis = .02 DM/kWh"
6200 PRINT
6210 PRINT" Verrechnungspreis = 31.80 DM":PRINT
6220 PRINT" Durchschnitts-":PRINT
6230 PRINT" Hoechstpreis      = .42 DM/kWh"
6240 PRINT" Mindestpreis      = .171 DM/kWh"
6250 PRINT:PRINT" Umsatzsteuer(Mwst.) = 14%"
6260 PRINT:PRINT" Jahresverbrauch = 1500 kWh"
6300 AP=.14:FL=89.4:VL=.02:VP=31.8
6310 DH=.42:DM=.171:US=14:JV=1500
6320 N=2000:MK=500
6330 SR$="ELEKTRO-ENERGIEKOSTEN-BERECHNUNG"
6400 GOTO 2010
7000 GOSUB 210
30130 REM -----------------------------
30140 REM ENERGIEKOSTEN-BERECHNUNG
32000 REM ----------------------------
32010 REM -    (C) 03.01.91          -
32020 REM -    MANFRED STEUDNER      -
32030 REM -    HEIDESTRASSE 166      -
32040 REM -    O-8142 RADEBERG       -
32050 REM -    AUF COMMODORE C64     -
32060 REM ----------------------------
32070 REM
32080 REM DS-KULTUR
32090 REM -spezial 911113
