1000 A=100:GOTO 20:REM AUTO BRANDSTOF VERBRUIK
1010 EV=VE:GH=HG:GV=VG:REM SCHERMGEGEVENS VASTHOUDEN
1020 REM DIM DATUM, KM-STAND, LITERS EN BENZ.PRIJS ETC
1030 AP=50:DIM TB(AP),DA(AP),KM(AP),LI(AP),BP(AP)
1040 DA=0:KM=0:LI=0:BP=0
1500 REM *** STARTSCHERM ***
1510 GOSUB 100:SR$="Auto-brandstofverbruik"
1520 GOSUB 270
1530 HO=5:VE=1:GOSUB 110:GOSUB 150
1540 VE=4:HO=8:GOSUB 110:PRINT "Informatie"
1550 VE=6:GOSUB 110:PRINT "Nieuw Bestand"
1560 VE=8:GOSUB 110:PRINT "Bestand aanvullen"
1570 VE=10:GOSUB 110:PRINT "Bestand laden"
1580 VE=12:GOSUB 110:PRINT "Bestand saven"
1590 VE=14:GOSUB 110:PRINT "Uitvoer naar scherm/printer"
1600 VE=16:GOSUB 110:PRINT "Wijzigingen aanbrengen"
1610 VE=18:GOSUB 110:PRINT "Stoppen"
1620 VE=VE+3:HO=6:GOSUB 110
1630 PRINT "Crsr Down = volgende regel"
1640 VE=VE+1:HO=6:GOSUB 110
1650 PRINT "Return    = keuze accoord"
1660 REM *** KEUZE UIT STARTSCHERM ***
1670 VE=4
1680 HO=5:GOSUB 110:PRINT ">";
1690 GOSUB 210:GOSUB 110:PRINT " ";
1700 IF IN=30 THEN VE=VE+2
1710 IF IN=31 THEN VE=VE-2
1720 IF VE>18 THEN VE=4
1730 IF VE<4 THEN VE=18
1740 IF IN<>13 THEN 1680
1750 VE=(VE-2)/2
1760 ON VE GOTO 7010,2010,2510,3510,4010,4510,6510,5300
2000 REM *** NIEUW BESTAND ***
2010 SR$="Nieuw Bestand opzetten"
2020 GOSUB 100:VE=1:HO=6:GOSUB 110:GOSUB 150
2030 SR$="Kan alleen na programmastart"
2040 VE=3:HO=6:GOSUB 110:GOSUB 150
2050 VE=7:HO=7:GOSUB 110:PRINT "Km-stand bij eerste"
2060 VE=9:GOSUB 110:PRINT "volle tank: "
2070 HO=27:VE=9:GOSUB 110:INPUT KL
2080 VE=11:HO=7:GOSUB 110:PRINT "Het kentekennummer:"
2090 HO=27:GOSUB 110:INPUT NF$
2100 VN=1:V=1:GOTO 2510
2500 REM *** INVOERROUTINE ***
2510 SR$="Invoeren gegevens":GOSUB 100:VE=1:HO=3:GOSUB 110
2520 GOSUB 150
2530 VE=3:HO=4:GOSUB 110:PRINT "Tankbeurt      : "
2540 VE=5:GOSUB 110:PRINT "Datum bv.870815: "
2550 VE=7:GOSUB 110:PRINT "Kilometerstand : "
2560 VE=9:GOSUB 110:PRINT "Getankte liters: "
2570 VE=11:GOSUB 110:PRINT "Brandstofprijs : "
2580 VE=3:HO=22:GOSUB 110:PRINT VN
2590 VE=5:HO=23:GOSUB 110:INPUT DA
2600 VE=7:GOSUB 110:INPUT KM
2610 VE=9:GOSUB 110:INPUT LI
2620 VE=11:HO=23:GOSUB 110:INPUT BP
2630 VE=15:HO=4:GOSUB 110:PRINT "Accoord      = Return"
2640 VE=17:GOSUB 110:PRINT "Verbeteren   = Spatie"
2650 VE=19:GOSUB 110:PRINT "Einde invoer = 0"
2660 GOSUB 200
2670 IF IN=32 THEN 2580
2680 IF IN=13 THEN 3010
2690 IF IN=48 THEN 3010
2700 GOTO 2660
3000 REM *** VERVOLG INVOER ***
3010 TB(V)=VN:DA(V)=DA:KM(V)=KM:LI(V)=LI:BP(V)=BP
3020 VN=VN+1:V=V+1
3030 IF IN=48 THEN GOSUB 100:GOTO 1530
3040 VE=5:HO=23
3050 FOR I=1 TO 4
3060 GOSUB 110:PRINT "          "
3070 VE=VE+2
3080 NEXT I
3090 VE=15:HO=4:GOSUB 110:PRINT "                      "
3100 VE=17:GOSUB 110:PRINT "                      "
3110 VE=19:GOSUB 110:PRINT "                      "
3120 GOTO 2580
3500 REM *** LOADROUTINES ***
3510 SR$="Load Programma":GOSUB 100
3520 VE=1:HO=8:GOSUB 110:GOSUB 150
3530 VE=4:HO=5:GOSUB 110:PRINT "Geef kentekennummer:"
3540 VE=4:HO=25:GOSUB 110:INPUT NF$
3550 VE=7:HO=5:GOSUB 110
3560 PRINT "Van disk              = 1"
3570 VE=9:GOSUB 110:PRINT "Van basicode cassette = 2"
3580 VE=11:GOSUB 110:PRINT "Naar hoofdmenu        = 3"
3590 VE=15:HO=7:GOSUB 110:PRINT "Geef uw keuze"
3600 HO=5:GOSUB 110
3610 GOSUB 210
3620 IF IN=49 THEN 3660
3630 IF IN=50 THEN 3820
3640 IF IN=51 THEN 1510
3650 REM *** VAN DISK ***
3660 GOSUB 100:VE=10:HO=9:SR$="Invoer van disc"
3670 GOSUB 110:GOSUB 150
3680 NF=4:GOSUB 500:GOSUB 3700
3690 GOSUB 580:GOTO 1510
3700 GOSUB 540:KL=VAL(IN$)
3710 GOSUB 540:V=VAL(IN$)
3720 GOSUB 540:VN=VAL(IN$)
3730 FOR S=1 TO V
3740 GOSUB 540:TB(S)=VAL(IN$)
3750 GOSUB 540:DA(S)=VAL(IN$)
3760 GOSUB 540:KM(S)=VAL(IN$)
3770 GOSUB 540:LI(S)=VAL(IN$)
3780 GOSUB 540:BP(S)=VAL(IN$)
3790 NEXT S
3800 RETURN
3810 REM *** VAN BASICODE-CASSETTE ***
3820 GOSUB 100:VE=10:HO=5:SR$="Invoer van basico-cass."
3830 GOSUB 110:GOSUB 150
3840 NF=0:GOSUB 500:GOSUB 3700
3850 GOSUB 580:GOTO 1510
4000 REM *** SAFEROUTINES ***
4010 SR$="Safe Programma":GOSUB 100
4020 VE=1:HO=8:GOSUB 110:GOSUB 150
4030 VE=4:HO=5:GOSUB 110
4040 PRINT "Naar disk              = 1"
4050 VE=6:GOSUB 110:PRINT "Naar basicode cassette = 2"
4060 VE=8:GOSUB 110:PRINT "Naar hoofdmenu         = 3"
4070 VE=15:HO=7:GOSUB 110:PRINT "Geef uw keuze"
4080 HO=5:GOSUB 110
4090 GOSUB 210
4100 IF IN=49 THEN 4140
4110 IF IN=50 THEN 4300
4120 IF IN=51 THEN 1510
4130 REM *** NAAR DISK ***
4140 GOSUB 100:VE=10:HO=9:SR$="Uitvoer naar disc"
4150 GOSUB 110:GOSUB 150
4160 NF=5:GOSUB 500:GOSUB 4170:GOTO 1510
4170 SR=KL:GOSUB 300:GOSUB 560
4180 SR=V:GOSUB 300:GOSUB 560
4190 SR=VN:GOSUB 300:GOSUB 560
4200 FOR S=1 TO V
4210 SR=TB(S):GOSUB 300:GOSUB 560
4220 SR=DA(S):GOSUB 300:GOSUB 560
4230 SR=KM(S):GOSUB 300:GOSUB 560
4240 SR=LI(S):GOSUB 300:GOSUB 560
4250 SR=BP(S):GOSUB 300:GOSUB 560
4260 NEXT S
4270 GOSUB 580
4280 RETURN
4290 REM *** NAAR BASICODE-CASSETTE ***
4300 GOSUB 100:VE=10:HO=5:SR$="Uitvoer naar basico-cass."
4310 GOSUB 110:GOSUB 150
4320 NF=1:GOSUB 500
4330 GOSUB 4170:GOTO 1510
4340 GOTO 5020
4500 REM *** TABEL NAAR SCHERM ***
4510 GOSUB 4760
4520 IF TB(1)<>0 THEN 4580
4530 VE=10:HO=8:SR$="Geen bestand aanwezig"
4540 GOSUB 100:GOSUB 110:GOSUB 150
4550 VE=20:HO=11:GOSUB 110:PRINT "Geef Return"
4560 VE=20:HO=9:GOSUB 110
4570 GOSUB 210:IF IN=13 THEN 1510
4580 GOSUB 100:SR$="Nr.*Datum *Km.st.*Lit.*Prijs*1 op"
4590 GOSUB 150
4600 VE=1:J=0
4610 FOR I=1 TO V
4620 J=J+1
4630 VE=VE+1:HO=2:GOSUB 110
4640 PRINT TB(I):HO=6:GOSUB 110:PRINT DA(I)
4650 HO=13:GOSUB 110:PRINT KM(I)
4660 IF I>=2 THEN EC=(KM(I)-KM(I-1))/LI(I)
4670 IF I=1 THEN EC=(KM(I)-KL)/LI(I)
4680 HO=20:GOSUB 110:PRINT LI(I)
4690 HO=25:GOSUB 110:PRINT BP(I)
4700 IF I<11 THEN GV=0
4710 SR=EC:CT=4:CN=1:GOSUB 310
4720 HO=32:GOSUB 110:PRINT SR$
4730 IF TB(I+1)=0 THEN 5000
4740 IF J=15 THEN J=0:GOSUB 4880
4750 NEXT I
4760 GOSUB 100:SR$=" Printer of Scherm  "
4770 VE=1:HO=8:GOSUB 110:GOSUB 150
4780 VE=4:HO=10:GOSUB 110:PRINT "Scherm             = 1"
4790 VE=6:GOSUB 110:PRINT "Printer            = 2"
4800 VE=8:GOSUB 110:PRINT "Grafiek op scherm  = 3"
4810 VE=10:GOSUB 110:PRINT "Grafiek op printer = 4"
4820 VE=13:HO=12:GOSUB 110:PRINT "Geef uw keuze"
4830 HO=10:GOSUB 110:GOSUB 210
4840 IF IN=50 THEN 5510
4850 IF IN=51 THEN 8010
4860 IF IN=52 THEN 9010
4870 RETURN
4880 VE=20:HO=9:GOSUB 110:PRINT "Druk op Return"
4890 VE=20:HO=3:GOSUB 110
4900 SR$="Nr.*Datum *Km.st.*Lit.*Prijs*1 op"
4910 GOSUB 210:GOSUB 100:VE=1:HO=0:GOSUB 150:RETURN
4920 TT=0:KK=0
4930 KK=KM(Z)-KM(Z-10)
4940 FOR B=Z-9 TO Z
4950 TT=TT+LI(B)
4960 NEXT B
4970 GV=KK/TT
4980 RETURN
5000 VE=19:HO=9:GOSUB 110:PRINT "Einde bestand"
5010 HO=9:VE=21:GOSUB 110
5020 PRINT "Druk op return"
5030 VE=21:HO=3:GOSUB 110
5040 GOSUB 210:GOSUB 100
5050 GOSUB 100:SR$="Totaaloverzicht":VE=1:HO=9:GOSUB 110
5060 GOSUB 150:TG=0:BL=0
5070 FOR E=1 TO V
5080 TG=TG+LI(E)
5090 BK=LI(E)*BP(E)
5100 BL=BL+BK
5110 NEXT E
5120 VE=4:HO=0:GOSUB 110:PRINT "Tot. gem. verbruik is 1 op :"
5130 KS=KM(V-1)-KL:EC=KS/TG:VE=4:HO=30:GOSUB 110:SR=EC
5140 CT=5:CN=1:GOSUB 310:PRINT SR$
5150 VE=6:HO=0:GOSUB 110:PRINT "Tot. aantal tankbeurten    :"
5160 VE=6:HO=30:GOSUB 110:PRINT V-1
5170 VE=8:HO=0:GOSUB 110:PRINT "Tot. aant. gereden km's    :"
5180 VE=8:HO=30:GOSUB 110:PRINT KM(V-1)-KL
5190 VE=10:HO=0:GOSUB 110
5200 PRINT "Tot. aant. getankte liters :"
5210 VE=10:HO=30:GOSUB 110:PRINT TG
5220 VE=12:HO=0:GOSUB 110
5230 PRINT "Totale brandstofkosten     :"
5240 VE=12:HO=30:GOSUB 110:CT=8:CN=2:SR=BL:GOSUB 310
5250 PRINT SR$:VE=14:HO=0
5260 GOSUB 110:PRINT "Brandstofkosten per km     :"
5270 CT=6:CN=3:SR=BL/(KM(V-1)-KL):GOSUB 310:HO=30:GOSUB 110
5280 PRINT SR$:VE=18:HO=3:GOSUB 110:PRINT "Druk op return"
5290 HO=0:GOSUB 110:GOSUB 210:GOSUB 100:GOTO 1510
5300 GOSUB 100:VE=12:HO=10:GOSUB 110
5310 SR$="Het ga U goed"
5320 GOSUB 150:SD=100:GOSUB 450:GOTO 950
5500 REM *** PRINTERROUTINE ***
5510 GOSUB 100
5520 SR$="Verbruikstabel van de "+NF$
5530 GOSUB 350:GOSUB 360
5540 VE=4:HO=5:GOSUB 110
5550 PRINT "Voer de tankbeurtnummers in,"
5560 VE=5:GOSUB 110:PRINT "waartussen het door U gewenste"
5570 VE=6:GOSUB 110:PRINT "gebied ligt."
5580 VE=9:GOSUB 110:PRINT "Laagste volgnummer:"
5590 VE=9:HO=30:GOSUB 110:INPUT L
5600 HO=5:GOSUB 110:PRINT "Hoogste"
5610 HO=30:GOSUB 110:INPUT H
5620 GOSUB 360:SR$=" Tb  Datum    Km.st Li.  Prys 1 op Gem."
5630 GOSUB 350:GOSUB 360:GOSUB 360
5640 FOR Z=L TO H
5650 CT=3:CN=0:SR=TB(Z):GOSUB 310:GOSUB 350
5660 CT=8:CN=0:SR=DA(Z):GOSUB 310:GOSUB 350
5670 CT=8:CN=0:SR=KM(Z):GOSUB 310:GOSUB 350
5680 CT=5:CN=1:SR=LI(Z):GOSUB 310:GOSUB 350
5690 CT=5:CN=2:SR=BP(Z):GOSUB 310:GOSUB 350
5700 IF Z>=2 THEN EC=(KM(Z)-KM(Z-1))/LI(Z)
5710 IF Z=1 THEN EC=(KM(Z)-KL)/LI(Z)
5720 CT=5:CN=1:SR=EC:GOSUB 310:GOSUB 350
5730 IF Z>10 THEN GOSUB 4920:GOTO 5770
5740 CT=2:CN=0:SR=0:GOSUB 310:GOSUB 350:GOSUB 360
5750 NEXT Z
5760 GOSUB 6010:GOTO 1510
5770 CT=5:CN=1:SR=GV:GOSUB 310:GOSUB 350:GOSUB 360
5780 NEXT Z
5790 GOSUB 6010:GOTO 1510
6000 REM *** TOTAAL OVERZICHT ***
6010 GOSUB 360:SR$="Totaalgegevens uit het bestand"
6020 GOSUB 350:GOSUB 360:SR$="xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
6030 GOSUB 350
6040 GOSUB 360:GOSUB 360:TG=0
6050 FOR C=1 TO V
6060 TG=TG+LI(C)
6070 NEXT C
6080 KS=KM(V-1)-KL:EC=KS/TG
6090 SR$="Totaal gemiddeld verbruik is 1 op :":GOSUB 350
6100 CT=8:CN=1:SR=EC:GOSUB 310:GOSUB 350:GOSUB 360
6110 SR$="Totaal aantal tankbeurten         :":GOSUB 350
6120 CT=6:CN=0:SR=V-1:GOSUB 310:GOSUB 350:GOSUB 360
6130 SR$="Totaal aantal verreden kilometers :":GOSUB 350
6140 CT=6:CN=0:SR=KM(V-1)-KL:GOSUB 310:GOSUB 350:GOSUB 360
6150 SR$="Totaal aantal getankte liters     :":GOSUB 350
6160 CT=8:CN=1:SR=TG:GOSUB 310:GOSUB 350:GOSUB 360
6170 BL=0
6180 FOR C=1 TO V
6190 BK=LI(C)*BP(C)
6200 BL=BL+BK
6210 NEXT C
6220 SR$="Totale brandstofkosten            :":GOSUB 350
6230 CT=9:CN=2:SR=BL:GOSUB 310:GOSUB 350:GOSUB 360
6240 SR$="Brandstofkosten per kilometer     :":GOSUB 350
6250 CT=10:CN=3:SR=BL/(KM(V-1)-KL):GOSUB 310
6260 GOSUB 350
6270 GOSUB 360:GOSUB 360
6280 RETURN
6500 REM *** WIJZIGINGEN AANBRENGEN ***RUN
6510 GOSUB 100:SR$="Wijzigingen aanbrengen"
6520 VE=1:HO=4:GOSUB 110:GOSUB 150
6530 VE=4:HO=6:GOSUB 110:PRINT "Welk tankbeurtnr :"
6540 VE=4:HO=26:GOSUB 110:INPUT RN
6550 VE=7:HO=6:GOSUB 110:PRINT "Tankbeurtnummer  :"
6560 VE=9:GOSUB 110:PRINT "Datum            :"
6570 VE=11:GOSUB 110:PRINT "Kilometerstand   :"
6580 VE=13:GOSUB 110:PRINT "Aantal liters    :"
6590 VE=15:GOSUB 110:PRINT "Brandstofkosten  :"
6600 VE=7:HO=27:GOSUB 110:PRINT TB(RN)
6610 VE=9:GOSUB 110:PRINT DA(RN)
6620 VE=11:GOSUB 110:PRINT KM(RN)
6630 VE=13:GOSUB 110:PRINT LI(RN)
6640 VE=15:GOSUB 110:PRINT BP(RN)
6650 VE=19:HO=6:GOSUB 110:PRINT "Voer correctie in"
6660 VE=9:HO=26:GOSUB 110:INPUT DA(RN)
6670 VE=11:GOSUB 110:INPUT KM(RN)
6680 VE=13:GOSUB 110:INPUT LI(RN)
6690 VE=15:GOSUB 110:INPUT BP(RN)
6700 VE=19:HO=6:GOSUB 110:PRINT "                      "
6710 GOSUB 110:PRINT "Nog een wijziging?"
6720 VE=20:GOSUB 110:PRINT "J(a) of N(ee)"
6730 GOSUB 210:IF IN=74 THEN 6510
6740 GOSUB 100:GOTO 1510
7000 REM ***INFO ***
7010 RESTORE 
7020 SR$="Programmainformatie"
7030 GOSUB 100:VE=1:HO=7:GOSUB 110:GOSUB 150:VE=3:HO=0
7040 GOSUB 110:GOSUB 7090
7050 VE=EV:HO=3:GOSUB 110
7060 PRINT "Druk op Spatiebalk";
7070 GOSUB 210:IF IN<>32 THEN 7070
7080 RETURN
7090 READ A$
7100 IF A$="*" THEN GOSUB 7050:GOTO 7030
7110 IF A$="***" THEN GOSUB 7050:GOTO 1510
7120 FOR A=1 TO LEN(A$):HO=HO+1:GOSUB 110
7130 B$=MID$(A$,A,1):PRINT B$
7140 NEXT A
7150 VE=VE+2:HO=0:GOSUB 110
7160 GOTO 7090
8000 REM *** GRAFIEK OP SCHERM ***
8010 GOSUB 8460
8020 GOSUB 600:VE=0:HO=0:CN=0:SR$="Verbruik":GOSUB 650
8030 HO=0.75:VE=0.95:SR$="Tankbeurt":GOSUB 650
8040 HO=0.1:VE=0.05:GOSUB 620:VE=0.88:GOSUB 630
8050 HO=0.998:GOSUB 630
8060 VE=0.90:HO=0
8070 FOR M=7 TO 23
8080 SR=M:VE=VE-0.05
8090 GOSUB 300
8100 GOSUB 650
8110 NEXT M
8120 HO=0.005:VE=0.90:GOSUB 620
8130 IF HW>TB(V-1) THEN HW=TB(V-1)
8140 FOR N=LW TO HW
8150 SR=N:GOSUB 300
8160 HO=HO+0.07
8170 GOSUB 650
8180 NEXT N
8190 HO=0.1:VE=0.05:GOSUB 620
8200 FOR Z=LW TO HW
8210 IF Z>=2 THEN EC=(KM(Z)-KM(Z-1))/LI(Z)
8220 IF Z=1 THEN EC=(KM(Z)-KL)/LI(Z)
8230 VE=0.86-(EC-7)*0.05
8240 IF HO=0.1 THEN GOSUB 620
8250 IF HO>0.1 THEN GOSUB 630
8260 HO=HO+0.07
8270 NEXT Z
8280 VE=0.05:HO=0.1:CN=0:GOSUB 620
8290 IF LW<11 THEN 8380
8300 FOR Z=LW TO HW
8310 GOSUB 4920
8320 VE=0.86-(GV-7)*0.05
8330 IF HO=0.1 THEN GOSUB 620
8340 IF HO>0.1 THEN GOSUB 630
8350 HO=HO+0.07
8360 NEXT Z
8370 VE=0.05:HO=0.1:CN=0:GOSUB 620
8380 GOSUB 210
8390 GOSUB 100:VE=3:HO=5:GOSUB 110
8400 PRINT "Nog een grafiek  = 1"
8410 VE=5:HO=5:GOSUB 110:PRINT "Hoofdmenu        = 2"
8420 VE=8:GOSUB 110
8430 GOSUB 210:IF IN=49 THEN 8010
8440 IF IN=50 THEN 1510
8450 GOTO 8430
8460 GOSUB 100:SR$="Grafisch overzicht op scherm"
8470 VE=0:HO=0:GOSUB 110:GOSUB 150:HO=1:VE=2:GOSUB 110
8480 PRINT "Geef de twee tankbeurtnummers"
8490 VE=3:HO=1:GOSUB 110
8500 PRINT "waartussen het door u gewenste"
8510 VE=4:HO=1:GOSUB 110
8520 PRINT "gebied ligt (max. 12 uit elkaar)."
8530 VE=8:HO=1:GOSUB 110:PRINT "Eerste tankbeurt"
8540 VE=8:HO=25:GOSUB 110:INPUT LW
8550 VE=9:HO=1:GOSUB 110:PRINT "Laatste tankbeurt"
8560 VE=9:HO=25:GOSUB 110
8570 INPUT HW:IF HW>LW+12 THEN 8590
8580 RETURN
8590 HO=5:VE=14:GOSUB 110:SR$="Bereik te groot"
8600 GOSUB 150:HO=3:GOSUB 110
8610 GOSUB 210:GOTO 8460
9000 REM *** GRAFIEK OP PRINTER ***
9010 SR$="Grafiek naar printer"
9020 GOSUB 100:VE=2:HO=7:GOSUB 110:GOSUB 150
9030 HO=8:VE=4:GOSUB 110:PRINT "Voer de tankbeurtnummers"
9040 VE=5:GOSUB 110:PRINT "in waarvan U een"
9050 VE=6:GOSUB 110:PRINT "grafisch overzicht"
9060 VE=7:GOSUB 110:PRINT "op pier wilt zien."
9070 VE=10:HO=10:GOSUB 110:PRINT "Laagste waarde"
9080 HO=28:GOSUB 110:INPUT LW
9090 HO=10:GOSUB 110:PRINT "Hoogste waarde"
9100 HO=28:GOSUB 110:INPUT HW
9110 VE=14:HO=8:GOSUB 110
9120 PRINT "Uw schatting van het gemid-"
9130 VE=15:GOSUB 110:PRINT "delde verbruik ca. 1 op :"
9140 HO=33:GOSUB 110:INPUT GM
9150 GOSUB 9370:GOSUB 9290
9160 S=0
9170 FOR Z=LW TO HW
9180 IF Z>=2 THEN EC=(KM(Z)-KM(Z-1))/LI(Z)
9190 IF Z=1 THEN EC=(KM(Z)-KL)/LI(Z)
9200 EC=4*(EC-AM):EC=INT(EC+0.5)
9210 CT=2:CN=0:SR=Z:GOSUB 300:GOSUB 310
9220 SR$=SR$+" ":GOSUB 350
9230 FOR Y=1 TO EC
9240 SR$="*":GOSUB 350
9250 NEXT Y
9260 GOSUB 360:S=S+1:IF S=20 THEN GOSUB 9290
9270 NEXT Z
9280 GOTO 1510
9290 AM=GM-5:BM=GM+5
9300 SR$="Tankbeurt / Verbruik":GOSUB 350:GOSUB 360
9310 GOSUB 360:SR$="  ":GOSUB 350
9320 FOR I=AM TO BM
9330 SR=I:CT=2:CN=0:GOSUB 310
9340 SR$=SR$+"  ":GOSUB 350
9350 NEXT I
9360 GOSUB 360:RETURN
9370 SR$="Grafisch verbruiksoverzicht van de "+NF$
9380 GOSUB 350:GOSUB 360:RETURN
25000 DATA "Dit programma werd gemaakt op een"
25010 DATA "Commodore 64."
25020 DATA "Het berekent en laat zien hoe het met"
25030 DATA "het brandstofverbruik staat van uw"
25040 DATA "voertuig."
25050 DATA "Het geeft de stand tussen 2 tankbeurten"
25060 DATA "en daarnaast in de rechter kolom"
25070 DATA "op papimr ook het doorsneegemiddelde"
25080 DATA "over de laatste 10 keer die u tankte.","*"
25090 DATA "Op het scherm laat het een totaal"
25100 DATA "overzicht zien van het gehele bestandR
27110 DATA "en bestaat uit de kolommen:Volgnummer,"
25120 DATA bDatum (wordt in het programma niet"
25130 DATA "bewerkt), kilometerstand, getankte"
25140 DATA "liters, brandstofprijs en gemiddeld"
25150 DATA "verbruik sinds de laatste tankbeurt."
25160 DATA "*"
25170 DATA "Op papier kunt u zelf bepalen welke"
25180 DATA "periode u in tabelvorm wilt doen"
25190 DATA "uitprinten."
25200 DATA"Beide tabellen worden gevolgd door een"
25210 DATA "overzicht dat het hele bestand"
25220 DATA "bestrijkt.","*"
25230 DATA "Het programma kan in zijn huidige vorm"
2=240 DATA"50 invoerberichten aan.":
25250 DATA "Indien u meer wilt invoeren, moeten de"
25260 DATA "dim-statements in regel 1030 wordmn"
25270 DATA "aangepast."
25280 DATA "Grafisch geeft het een overzicht over"
25290 DATA "max 12 tankbeurten naar keuze. Als de"
25300 DATA "laagste ingevoerde beurt hoger is dan"
25310 DATA "10, geeft de grafiek een tweede lijn","*"
25320 DATA "die het voortschrijdende gemiddelde"
25330 DATA "over de laatste 10x tankm~ geeft."
25340 DCTA"Deze 12 {mer geldt voor o.a. de C64,"
25350 DATA "voor andere computers regel 8520 en"
25360 DATA "de inforegel 8570 aanpassen.","*"
25430 DATA "Bij het starten van een nieuw bestand"
25440 DATA "wordt alleen de km-stand gevraagd"
25450 DATA "waarbij U de laatste keer de tank"
25460 DATA "geheel vol aftankte."
25470 DATA "Het programma loopt dan door naar"
25480 DATA "de rubriek - Bestand aanvullen - voor"
25490 DATA "gegevens van de eerstvolgende tankbeurt"
25500 DATA "*"
25510 DATA "Ik hoop dat U veel plezier aan het"
25520 DATA "programma beleeft."
25530 DATA "Altijd open voor opbouwende kritiek,"
25540 DATA "verblijf ik met de beste groeten,"
25550 DATA "     ==========ý===============?"
25560 DATC"              P H J Kasel"
25570 DATA "     ==========================="
25580 DATA "***"
32000 REM *      P H J KASEL       *
32010 REM *    KROMMEHAGEN 574     *
32020 REM *   3078 AS ROTTERDAM    *
32030 REM 
32040 REM TROS-RADIO dd 880504
