1000 A=500:GOTO 20:REM Loonberekeningen 1988
1010 VT=VE:MN=0
1015 GOSUB 100:GOSUB 30000
1020 REM STUURMODULE
1030 REM INITIALISEREN
1040 DIM L(21),M(9),BT$(4),LD(18,10),PD(18,10)
1050 DIM TB(8),TP(8),TL(8)
1060 PRINT"Even geduld a.u.b."
1070 P9=0
1080 FOR J=0 TO 18:FOR K=0 TO 10
1090 READ LD(J,K):READ PD(J,K)
1100 NEXT K:NEXT J
1110 FOR K=0 TO 8
1120 READ TB(K):READ TP(K):READ TL(K)
1130 NEXT K
1140 BE=0:BN=0:U=0:P6=5.1:P5=13.3
1150 BT$(0)="de WITTE tabel"
1160 BT$(1)="de ROSE tabel"
1170 BT$(2)="de GROENE tabel"
1180 BT$(3)="de GRIJZE tabel"
1190 BT$(4)="de GELE tabel"
1200 REM NAAR SUBROUTINES
1210 GOSUB 1500:REM Menu in beeld
1220 GOSUB 2000:REM Tabelkeuze
1230 IF MN>2 THEN GOSUB 2910:REM Nadere inp.bijz.bel.
1240 GOSUB 2200:REM Keuze tariefgroep
1250 GOSUB 3510:REM Max.+ berek. soc.premies I
1260 IF MN>2 THEN GOSUB 3300:REM Afwijkende maxima
1270 GOSUB 3750:REM Max. en berek.soc. premies II
1280 IF MN<3 THEN GOSUB 4000:REM Tabelformules toepassen
1290 IF (MN>2) AND (TB=1) THEN GOSUB 4500:REM Byz.tar.WITTE tabel
1300 IF (MN>2) AND (TB=2) THEN GOSUB 4600:REM Byz.tar.ROSE tabel
1310 IF (MN>2) AND (TB=3) THEN GOSUB 4770:REM Byz.tar.GROENE tabel
1320 IF (MN>2) AND (TB=4) THEN GOSUB 4850:REM Byz.tar.GRIJZE tabel
1330 IF (MN>2) AND (TB=5) THEN GOSUB 4900:REM Byz.tar.GELE tabel
1340 GOSUB 5050:REM Premie AOW/AWW en Loonbel.berekenen
1350 GOSUB 5150:REM Berekening op scherm
1360 GOTO 5600:REM Printer-routines
1370 REM Benadering NETTO--->BRUTO
1380 NV=L(20)-L(15):REM Verschil gewenst/bereikt netto
1390 IF BN=1 THEN 1350:REM Gezocht bruto bereikt
1400 IF ABS(NV)<.005 THEN BN=1:GOTO 1250
1410 IF ABS(NV)<10 THEN L(0)=L(0)+NV:BE=BE+1:GOTO 1430
1420 L(0)=L(0)+L(0)/L(15)*NV:BE=BE+1:REM Ophogen bruto
1430 HO=10:VE=10:GOSUB 110:PRINT"Even geduld a.u.b."
1440 GOTO 1250
1490 REM MENU IN BEELD
1500 GOSUB 100:MN=0:N=0:L(0)=0:L(20)=0:X$=""
1510 T1$="":T2$="":T3$="":T4$="":T6$=""
1520 PRINT"  SALARISBEREKENINGEN 1988"
1530 PRINT"  ------------------- ====":PRINT
1540 PRINT"   PERIODIEKE BELONINGEN":PRINT
1550 PRINT"     1 Van BRUTO naar NETTO"
1560 PRINT"     2 Van NETTO naar BRUTO":PRINT
1570 PRINT"   BIJZONDERE BELONINGEN:":PRINT
1580 PRINT"     3 Van BRUTO naar NETTO"
1590 PRINT"   en: Van NETTO naar BRUTO":PRINT
1600 PRINT"     4      STOPPEN"
1610 PRINT:PRINT"   Welk nummer ";:INPUT MN
1620 IF (MN<1) OR (MN>4) THEN 1500
1630 IF MN=4 THEN 1015
1640 REM ALGEMENE INPUT
1650 GOSUB 100:PRINT"Naam werknemer: ";:INPUT T1$
1660 PRINT"Loonstaat nummer: ";:INPUT T2$
1670 PRINT"Fiskaal nummer: ";:INPUT T3$
1680 IF MN=3 THEN GOSUB 100:RETURN
1690 PRINT"Ingangsdatum: ";:INPUT T4$
1700 PRINT"De loontijdvakken zijn:"
1710 PRINT"   1  per dag"
1720 PRINT"   2  per week"
1730 PRINT"   3  per maand"
1740 PRINT"Welk nummer";:INPUT N
1750 IF (N<1) OR (N>3) THEN GOSUB 100:GOTO 1700
1760 IF N=1 THEN T6$="dag"
1770 IF N=2 THEN T6$="week"
1780 IF N=3 THEN T6$="maand"
1790 X$=LEFT$(T6$,1):T6$="per "+T6$
1800 IF MN=1 THEN PRINT"Geef BRUTO-bedrag svp";:INPUT L(0)
1810 IF MN=2 THEN PRINT"Geef NETTO-bedrag svp";:INPUT L(20)
1820 IF N=1 THEN 1850
1830 PRINT"Op hoeveel dagen werkt werknemer"
1835 PRINT"PER WEEK";:INPUT DG:IF DG>5 THEN DG=5
1840 DG=DG/5
1850 GOSUB 7650
1860 IF SR<>1 THEN 1500
1870 IF MN=2 THEN L(0)=L(20)*1.3:REM 1e benadering bruto
1880 RETURN
1990 REM TABELKEUZE
2000 GOSUB 100:PRINT"Welke tabel is van toepassing:"
2010 N=0:M(8)=0:L(12)=0
2020 PRINT:PRINT"1  ";BT$(0)
2030 PRINT"2  ";BT$(1)
2040 PRINT"3  ";BT$(2)
2050 PRINT"4  ";BT$(3)
2060 PRINT"5  ";BT$(4)
2070 PRINT:PRINT"Kies nummer ";:INPUT TB
2080 U=0:UU=0
2090 IF (TB<1) OR (TB>5) THEN 2000
2100 IF (TB=1) OR (TB>3) THEN 2140
2110 PRINT"Is in Uw loon etc. uitkering A.W.W."
2120 PRINT"inbegrepen";:GOSUB 7660:IF SR=1 THEN U=14039
2130 IF SR=1 THEN PRINT"Hoeveel op JAARBASIS";:INPUT UU
2140 PRINT"Toets H voor uitleg bij tabel"
2150 PRINT"Toets V voor verdere verwerking.":GOSUB 210
2160 IF IN=72 THEN 7500
2170 GOSUB 100:RETURN
2200 PRINT"      DE TARIEFGROEPEN ZIJN: "
2210 PRINT"   1    I-algemeen"
2220 PRINT"   2    I-met aanvull.arb.toeslag"
2230 PRINT"   3   II-algemeen"
2240 PRINT"   4  III-algemeen"
2250 PRINT"   5   IV-algemeen"
2260 PRINT"   6   IV-met aanvull.arb.toeslag"
2270 PRINT:PRINT"Welk nummer ";:INPUT N
2280 IF (N<1) OR (N>6) THEN GOSUB 100:GOTO 2200
2290 IF N=1 THEN T8$="I"
2300 IF N=2 THEN T8$="IA"
2310 IF N=3 THEN T8$="II"
2320 IF N=4 THEN T8$="III"
2330 IF N=5 THEN T8$="IV"
2340 IF N=6 THEN T8$="IVA"
2350 IF (MN<3) OR (TB>3) THEN 2400
2360 PRINT"Moet de inhouding premie AOW/AWW"
2370 PRINT"beperkt worden i.v.m. het jaarmaximum"
2380 PRINT"ad f 8.352,--";:GOSUB 7660
2390 IF SR=1 THEN PRINT"Tot hoeveel";:INPUT M(8)
2400 PRINT"Vermindering Inspect.toepassen";:GOSUB 7660
2410 IF SR=1 THEN PRINT"Hoeveel";:INPUT L(12)
2420 GOSUB 7650
2430 IF SR=2 THEN 2200:REM NEE
2440 REM INPUT SOCIALE PREMIES I
2450 BS=0:BV=0:BB=0:LS=0:LV=0:LB=0
2460 P1=0:P2=0:P3=0:P4=0:P6=5.1:GOSUB 100
2470 PRINT"Premie Soc. Fonds berekenen";:GOSUB 7660
2480 IF SR=2 THEN 2550
2490 PRINT"In procenten van het loon";:GOSUB 7660
2500 IF SR=2 THEN GOSUB 7550:INPUT BS:GOTO 2550
2510 GOSUB 7560:INPUT P1
2520 PRINT"Berekenen over afwijkend loon";:GOSUB 7660
2530 IF SR=2 THEN 2550
2540 PRINT"Hoeveel ";T6$;:INPUT LS
2550 PRINT"Premie VUT-regeling berekenen";:GOSUB 7660
2560 IF SR=2 THEN 2630
2570 PRINT"In procenten van het loon";:GOSUB 7660
2580 IF SR=2 THEN GOSUB 7550:INPUT BV:GOTO 2630
2590 GOSUB 7560:INPUT P2
2600 PRINT"Berekenen over afwijkend loon";:GOSUB 7660
2610 IF SR=2 THEN 2630
2620 PRINT"Hoeveel ";T6$;:INPUT LV
2630 PRINT"Premie Pens.fonds berekenen";:GOSUB 7660
2640 IF SR=2 THEN 2720
2650 PRINT"In procenten van het loon";:GOSUB 7660
2660 IF SR=2 THEN GOSUB 7550:INPUT BB:GOTO 2720
2670 GOSUB 7560:INPUT P3
2680 PRINT"Berekenen over afwijkend loon";:GOSUB 7660
2690 IF SR=2 THEN 2720
2700 PRINT"Hoeveel ";T6$;:INPUT LB
2710 REM INPUT SOCIALE PREMIES II
2720 PRINT"Premie ZFW van toepassing";:GOSUB 7660
2730 IF SR=2 THEN P6=0
2740 PRINT"Perc. ZFW dus ";P6
2750 PRINT"Inhoudingspercentage premie ZW+WW ";:INPUT P4
2760 PRINT"Inhoudingspercentage WAO is ";P5
2770 GOSUB 7650
2780 IF SR=2 THEN 2450
2790 GOSUB 100:RETURN
2900 REM NADERE INPUT BIJZ.BELONINGEN
2910 N=0:P9=0:L(0)=0:L(18)=0:L(19)=0:L(20)=0:T5$="":T7$=""
2920 PRINT"Geef omschrijving bijz. beloning"
2930 PRINT" 1 Vakantietoeslag"
2940 PRINT" 2 Uw omschrijving (max.13 tekens svp.)"
2950 PRINT:PRINT"Welk nummer ";:INPUT N
2960 IF (N<1) OR (N>2) THEN GOSUB 100:GOTO 2920
2970 IF N=2 THEN PRINT"Geef omschrijving svp ";:INPUT T5$
2980 IF N=1 THEN T5$="vak.toeslag"
2990 PRINT"Voor welke periode is de betaling ";:INPUT T7$
3000 PRINT"Is de beloning vastgesteld op:"
3010 PRINT" 1  Een BRUTO percentage"
3020 PRINT" 2  Een NETTO percentage"
3030 PRINT" 3  Een BRUTO geldbedrag"
3040 PRINT" 4  Een NETTO geldbedrag"
3050 PRINT:PRINT"Welk nummer ";:INPUT N
3060 IF (N<1) OR (N>4) THEN GOSUB 100:GOTO 3000
3070 IF (N=1) OR (N=2) THEN PRINT"Welk percentage ";:INPUT P9
3080 IF N=3 THEN PRINT"Hoeveel BRUTO";:INPUT L(0):GOTO 3150
3090 IF N=4 THEN PRINT"Hoeveel NETTO";:INPUT L(20)
3100 IF N=4 THEN L(0)=L(20)*1.3:GOTO 3130
3110 IF (N=1) OR (N=2) THEN PRINT"Over welk bedrag ";
3120 INPUT L(18)
3130 IF N=1 THEN L(0)=INT(P9*L(18))/100
3140 IF N=2 THEN L(20)=INT(P9*L(18))/100:L(0)=L(20)*1.3
3150 PRINT"(Basisloon voor toepassing van het bij-"
3160 PRINT" zondere tarief is JAARloon kolom 10"
3170 PRINT" van de loonstaat 1987)"
3180 PRINT"Geef basisloon s.v.p. ";:INPUT L(19)
3190 GOSUB 7650
3200 IF SR=2 THEN GOSUB 100:GOTO 2910
3210 GOSUB 100:RETURN
3290 REM AFWIJKENDE MAXIMA SOC.WETT.
3300 GOSUB 100:IF BE>0 THEN GOSUB 3750:RETURN
3310 WA=0:ZW=0:ZF=0
3320 PRINT"WAO-franchise van toepassing";:GOSUB 7660
3330 IF SR=2 THEN 3370
3340 PRINT"Geef franchise-bedrag"
3350 PRINT"Als dit gelijk mag zijn aan brutoloon"
3360 PRINT"dan 1 intoetsen s.v.p. ":INPUT WA
3370 PRINT"Afwijkend max.loon voor ZW/WW toepassen";
3380 GOSUB 7660:IF SR=2 THEN 3400
3390 PRINT"Geef dit max. loon svp ";:INPUT ZW
3400 IF P6=0 THEN ZF=M(6):GOTO 3440
3410 PRINT"Afwijkend max.loon voor  ZFW  toepassen";
3420 GOSUB 7660:IF SR=2 THEN 3440
3430 PRINT"Geef dit max. loon svp ";:INPUT ZF
3440 GOSUB 7650
3450 IF SR=2 THEN GOSUB 100:GOTO 3300
3500 REM MAXIMA SOC.PREMIES I
3510 IF X$="d" THEN FOR X=0 TO 5:M(X)=263:NEXT X
3520 IF X$="d" THEN M(6)=162:M(7)=91
3530 IF X$="w" THEN FOR X=0 TO 5:M(X)=DG*1320.06:NEXT X
3540 IF X$="w" THEN M(6)=DG*813.12:M(7)=DG*456.75
3550 IF X$="m" THEN FOR X=0 TO 5:M(X)=DG*5720.25:NEXT X
3560 IF X$="m" THEN M(6)=DG*3523.5:M(7)=DG*1979.25
3570 MF=M(6):MZ=M(0):MW=M(0)-M(7)
3580 IF M(0)>L(0) THEN M(0)=L(0)
3590 FOR X=1 TO 3:M(X)=M(0):NEXT X
3600 REM BEREKENING SOC. PREMIES I
3610 M(1)=L(0):IF LS<>0 THEN M(1)=LS
3620 L(1)=(INT(P1*M(1)))/100:REM Berekening premie Soc.Fds.
3630 IF BS<>0 THEN L(1)=BS
3640 M(2)=L(0):IF LV<>0 THEN M(2)=LV
3650 L(2)=(INT(P2*M(2)))/100:REM Berekening VUT-premie
3660 IF BV<>0 THEN L(2)=BV
3670 M(3)=L(0):IF LB<>0 THEN M(3)=LB
3680 L(3)=(INT(P3*M(3)))/100:REM Berekening premie BP.Fds
3690 IF BB<>0 THEN L(3)=BB
3700 L(9)=L(0)-L(2)-L(3):RETURN
3740 REM MAXIMA SOC.PREMIES II
3750 IF MN<3 THEN FOR X=4 TO 6:M(X)=L(9):NEXT X:GOTO 3830
3760 M(4)=L(9):IF ZF>0 THEN M(4)=ZW
3770 M(6)=L(9):IF ZW>0 THEN M(6)=ZF
3780 M(7)=L(9):IF WA=0 THEN M(7)=0
3790 IF WA>1 THEN M(7)=WA
3800 IF M(4)>L(9) THEN M(4)=L(9)
3810 IF M(6)>L(9) THEN M(6)=L(9)
3820 IF M(7)>L(9) THEN M(7)=L(9)
3830 M(5)=L(9)-M(7):IF M(5)>L(9) THEN M(5)=L(9)
3840 IF (MN<3) AND (M(4)>MZ) THEN M(4)=MZ
3850 IF (MN<3) AND (M(5)>MW) THEN M(5)=MW
3860 IF (MN<3) AND (M(6)>MF) THEN M(6)=MF
3870 REM BEREKENING SOC. PREMIES II
3880 L(4)=(INT(P4*M(4)))/100:REM Berekening premie ZW/WW
3890 IF M(5)<0 THEN M(5)=0
3900 L(5)=(INT(P5*M(5)))/100:REM Berekening premie WAO
3910 L(6)=(INT(P6*M(6)))/100:REM Berekening premie ZFW
3920 REM BEREKENING FISKAAL LOON
3930 L(10)=L(9)-L(4)-L(5)+L(6)
3940 L(11)=0:FOR X=1 TO 6:L(11)=L(11)+L(X):NEXT X:REM Totaal SL
3950 IF L(12)>L(10) THEN L(12)=L(10):REM loon=max.Verm.Insp.
3960 L(13)=L(10)-L(12):REM Loon na aftrek verm. Insp.
3970 RETURN
3990 REM TABELFORMULES
4000 IF T8$="I" THEN D=7803:S=6653:T=46.58
4010 IF (T8$="I") AND (TB>1) THEN D=7392:REM tab.niet WIT
4020 IF T8$="IA" THEN D=8597:S=6653:T=46.58
4030 IF (T8$="IA") AND (TB>1) THEN D=0:REM tab.niet WIT
4040 IF T8$="II" THEN D=11499:S=9980:T=69.86
4050 IF (T8$="II") AND (TB>1) THEN D=11088:REM tab.niet WIT
4060 IF T8$="III" THEN D=15195:S=13306:T=93.15
4070 IF (T8$="III") AND (TB>1) THEN D=14784:REM tab.niet WIT
4080 IF T8$="IV" THEN D=13717:S=11976:T=83.84
4090 IF (T8$="IV") AND (TB>1) THEN D=13306:REM tab.niet WIT
4100 IF T8$="IVA" THEN D=18185:S=11976:T=83.84
4110 IF (T8$="IVA") AND (TB>1) THEN D=0:REM tab.niet WIT
4120 IF X$="m" THEN F=12:PO=0
4130 IF X$="w" THEN F=52:PO=.51
4140 IF X$="d" THEN F=260:PO=2.59
4150 JL=INT(F*L(13)+PO)
4160 TT=60
4170 IF JL>75360 THEN TT=480
4180 IF JL>134400 THEN TT=960
4190 I=INT(JL/TT)*TT
4200 K=INT(.04*I):IF K<200 THEN K=200
4210 IF K>800 THEN K=800
4220 M=200:N=200:P=.128:Q=1.039
4230 RH=INT((I-UU-M)/(1+P*Q)):R=INT(RH+U-S/T)
4240 IF R<0 THEN R=0
4250 IF R>100 THEN R=100
4260 IF TB>3 THEN P=0:Q=0
4270 IF TB=1 THEN Y=INT(P*(I-K-N)):REM WITTE TABEL
4280 IF TB=2 THEN Y=INT(P*R/100*RH):REM ROSE TABEL
4290 IF TB=3 THEN Y=INT(P*(I-M)):REM GROENE TABEL
4300 IF Y>8352 THEN Y=8352
4310 IF Y<29.01 THEN Y=0
4320 IF TB=1 THEN G=I-K-N-Y-D:REM WITTE tabel
4330 IF TB=2 THEN G=I-M-INT((1+Q)*Y)
4340 IF (TB=2) AND (UU=0) THEN G=G-D:REM ROSE TABEL
4350 IF TB=3 THEN G=I-UU-M-Y
4360 IF (TB=3) AND (UU=0) THEN G=G-D:REM GROENE TABEL
4370 IF TB=4 THEN G=I:REM GRIJZE tabel
4380 IF TB=5 THEN G=I-M-D:REM GELE tabel
4390 FOR K=8 TO 0 STEP -1
4400 IF G>TB(K) THEN A=TB(K):B=TP(K):C=TL(K)
4410 NEXT K
4420 K=0
4430 X=INT(((G-A)*B)/100)+C
4440 IF (X<139.01) AND (TB<>4) THEN X=0
4450 X=X/F:Y=Y/F
4460 X=INT(X*100)/100:IF X$="m" THEN X=INT(X*10)/10
4470 Y=INT(Y*100)/100:IF X$="m" THEN Y=INT(Y*10)/10
4480 RETURN
4490 REM BIJZONDER TARIEF WITTE TABEL
4500 P7=12.8:IF (L(19)>=5600) AND (L(19)<22300) THEN P7=12.2
4510 IF T8$="I" THEN T8=0:GOTO 4960
4520 IF T8$="IA" THEN T8=1:GOTO 4960
4530 IF T8$="II" THEN T8=2:GOTO 4960
4540 IF T8$="III" THEN T8=3:GOTO 4960
4550 IF T8$="IV" THEN T8=4:GOTO 4960
4560 IF T8$="IVA" THEN T8=5:GOTO 4960
4590 REM BIJZONDER TARIEF ROSE TABEL
4600 P7=11.2
4610 IF T8$="I" THEN 4650
4620 IF T8$="II" THEN 4670
4630 IF T8$="III" THEN 4690
4640 IF T8$="IV" THEN 4710
4650 IF (L(19)<6900) AND (UU=0) THEN P7=0
4660 T8=6:GOTO 4960
4670 IF (L(19)<10200) AND (UU=0) THEN P7=0
4680 T8=7:GOTO 4960
4690 IF (L(19)<13600) AND (UU=0) THEN P7=0
4700 T8=8:GOTO 4960
4710 IF (L(19)<12200) AND (UU=0) THEN P7=0
4720 T8=9:GOTO 4960
4760 REM BIJZONDER TARIEF GROENE TABEL
4770 P7=12.8
4780 IF T8$="I" THEN T8=10:GOTO 4960
4790 IF T8$="II" THEN T8=11:GOTO 4960
4800 IF T8$="III" THEN T8=12:GOTO 4960
4810 IF T8$="IV" THEN T8=13:GOTO 4960
4840 REM BIJZONDER TARIEF GRIJZE TABEL
4850 P7=0:T8=14:GOTO 4960
4890 REM BIJZONDER TARIEF GELE TABEL
4900 P7=0
4910 IF T8$="I" THEN T8=15:GOTO 4960
4920 IF T8$="II" THEN T8=16:GOTO 4960
4930 IF T8$="III" THEN T8=17:GOTO 4960
4940 IF T8$="IV" THEN T8=18:GOTO 4960
4950 REM BEPAAL P8 BIJ TARIEFGROEP T8
4960 K=0
4970 IF L(19)>=100*LD(T8,K) THEN P8=PD(T8,K):K=0:RETURN
4980 K=K+1:IF K<11 THEN 4970
4990 P8=0:RETURN
5000 IF BE>0 THEN RETURN
5040 REM BEREK. AOW/AWW EN L.B.
5050 IF MN<3 THEN L(7)=Y:L(8)=X
5060 IF MN=3 THEN L(7)=(INT(P7*L(13)))/100
5070 IF MN=3 THEN L(8)=(INT(P8*L(13)))/100
5080 IF (M(8)<>0) AND (M(8)<L(7)) THEN L(7)=M(8)
5090 REM BEREKENING NETTO BEDRAG
5100 L(14)=L(11)+L(7)+L(8):L(15)=L(0)-L(14)
5110 IF L(20)<>0 THEN 1380
5120 RETURN
5140 REM BEREKENING OP SCHERM
5150 GOSUB 100:IF T6$="" THEN T6$=T5$
5160 SR=INT(L(0)):GOSUB 300:CT=LEN(SR$)+3:CN=2:K3=39-CT
5170 K2=K3+2-CT:K1=K2-CT:SR=L(0):GOSUB 310:PRINT"Bruto ";T6$;
5180 S$="":DS$="":FOR X=1 TO CT:S$=S$+"-":DS$=DS$+"=":NEXT X
5190 PRINT TAB(K1);SR$;:PRINT TAB(K3);SR$:SR=M(1):GOSUB 310
5200 IF BS<>0 THEN PRINT"SF  vast bedrag";:SR=L(1):GOSUB 310
5210 IF BS=0 THEN PRINT"SF ";P1;"%";SR$;:SR=L(1):GOSUB 310
5220 PRINT TAB(K2);SR$:SR=M(3):GOSUB 310
5230 IF BB<>0 THEN PRINT"BPF vast bedrag";:SR=-L(3):GOSUB 310
5240 IF BB=0 THEN PRINT"BPF";P3;"%";SR$;:SR=-L(3):GOSUB 310
5250 PRINT TAB(K1);SR$;:SR=L(3):GOSUB 310
5260 PRINT TAB(K2);SR$:SR=M(2):GOSUB 310
5270 IF BV<>0 THEN PRINT"VUT vast bedrag";:SR=-L(2):GOSUB 310
5280 IF BV=0 THEN PRINT"VUT";P2;"%";SR$;:SR=-L(2):GOSUB 310
5290 PRINT TAB(K1);SR$;:SR=L(2):GOSUB 310
5300 PRINT TAB(K2);SR$:PRINT TAB(K1);S$
5310 SR=L(9):GOSUB 310:PRINT TAB(K1);SR$
5320 SR=M(6):GOSUB 310:PRINT"ZFW";P6;"%";SR$;
5330 SR=L(6):GOSUB 310:PRINT TAB(K1-1);"+";SR$;
5340 PRINT TAB(K2);SR$:SR=M(4):GOSUB 310
5350 PRINT"ZWW";P4;"%";SR$;:SR=-L(4):GOSUB 310
5360 PRINT TAB(K1);SR$;:SR=L(4):GOSUB 310:PRINT TAB(K2);SR$
5370 SR=M(5):GOSUB 310:PRINT"WAO";P5;"%";SR$;:SR=-L(5)
5380 GOSUB 310:PRINT TAB(K1);SR$;:SR=L(5):GOSUB 310
5390 PRINT TAB(K2);SR$:PRINT TAB(K1);S$:SR=L(10)
5400 GOSUB 310:PRINT"Fiskaal loon";TAB(K1);SR$:SR=L(11)
5410 GOSUB 310:PRINT TAB(K1);DS$;TAB(K2);S$
5420 PRINT"Totaal soc.lasten";TAB(K2);SR$:SR=L(13):GOSUB 310
5430 PRINT"AOW/AWW ";SR$;:SR=L(7):GOSUB 310
5440 PRINT TAB(K2);SR$:SR=L(13):GOSUB 310
5450 PRINT"Loonbel./";T8$;" ";SR$;:SR=L(8):GOSUB 310
5460 PRINT TAB(K2);SR$:PRINT TAB(K2);S$
5470 SR=L(14):GOSUB 310
5480 PRINT"Inhoudingen";TAB(K3);SR$
5490 PRINT TAB(K3);S$
5500 SR=L(15):GOSUB 310:PRINT"Netto ";T6$;TAB(K3);SR$
5510 PRINT TAB(K3);DS$
5520 IF (MN=3) AND (P9<>0) THEN L(18)=L(0)*100/P9:CT=8
5530 GOSUB 7750:GOSUB 100
5540 SR=L(18):GOSUB 310
5550 IF P9<>0 THEN PRINT"Berekend is:";P9;"% van bruto ";SR$
5560 IF P9<>0 THEN SR=L(15)*100/P9:GOSUB 310
5570 IF P9<>0 THEN PRINT"resp. ";P9;
5580 IF P9<>0 THEN PRINT"% van netto ";SR$
5590 REM PRINTROUTINE
5600 PRINT"De printer-routine is opgenomen in het"
5610 PRINT"2e deel van dit programmma en kan zo-"
5620 PRINT"nodig worden toegevoegd met MERGE o.d."
5630 GOTO 1000
6700 PRINT"Deze toelichting staat in het 2e deel"
6710 PRINT"en kan zonodig met MERGE o.d. worden"
6720 PRINT"toegevoegd."
6730 GOSUB 7570:IF IN<>86 THEN GOSUB 100:GOTO 6700
6750 GOTO 2000
6830 PRINT"Deze toelichting staat in het 2e deel"
6840 PRINT"en kan zonodig met MERGE o.d. worden"
6850 PRINT"toegevoegd."
6860 GOSUB 7570:IF IN<>86 THEN GOSUB 100:GOTO 6700
6880 GOTO 2000
7000 PRINT"Deze toelichting staat in het 2e deel"
7010 PRINT"en kan zonodig met MERGE o.d. worden"
7020 PRINT"toegevoegd."
7030 GOSUB 7570:IF IN<>86 THEN GOSUB 100:GOTO 6700
7050 GOTO 2000
7160 PRINT"Deze toelichting staat in het 2e deel"
7170 PRINT"en kan zonodig met MERGE o.d. worden"
7180 PRINT"toegevoegd."
7190 GOSUB 7570:IF IN<>86 THEN GOSUB 100:GOTO 6700
7210 GOTO 2000
7300 PRINT"Deze toelichting staat in het 2e deel"
7310 PRINT"en kan zonodig met MERGE o.d. worden"
7320 PRINT"toegevoegd."
7330 GOSUB 7570:IF IN<>86 THEN GOSUB 100:GOTO 6700
7350 GOTO 2000
7500 ON TB GOTO 6700,6830,7000,7160,7300
7550 PRINT"Welk BEDRAG in te houden ";T6$;:RETURN
7560 PRINT"Welk percentage inhouding ";T6$;:RETURN
7570 PRINT"Toets  V  voor verdere verwerking ";:GOSUB 210
7580 RETURN
7650 PRINT"Alle gegevens goed ingevuld";
7660 PRINT" <J/N> ";:SR=0
7670 GOSUB 210
7680 IF IN=74 THEN SR=1:PRINT IN$:RETURN
7690 IF IN=78 THEN SR=2:PRINT IN$:RETURN
7700 GOSUB 250:GOTO 7670
7750 VE=VT:HO=0:GOSUB 110
7760 PRINT"<Toets voor vervolg>";:GOSUB 210:RETURN
25000 DATA 2742,72,1534,69,1207,66,914,60,737,51,678,44.4
25010 DATA 513,35.7,329,27.9,234,20.9,223,12.2,120,11.7
25020 DATA 2751,72,1543,69,1215,66,923,60,737,51,688,44.4
25030 DATA 523,35.7,339,27.9,244,20.9,223,12.2,130,11.7
25040 DATA 2784,72,1575,69,1248,66,955,60,737,51,725,44.4
25050 DATA 560,35.7,376,27.9,281,20.9,223,12.2,169,11.7
25060 DATA 2825,72,1616,69,1289,66,996,60,768,51,727,41
25070 DATA 607,35.7,423,27.9,328,20.9,223,12.2,218,11.7
25080 DATA 2808,72,1600,69,1272,66,980,60,752,51,737,41
25090 DATA 588,35.7,404,27.9,309,20.9,223,12.2,198,11.7
25100 DATA 2858,72,1649,69,1322,66,1029,60,801,51,737,41
25110 DATA 645,35.7,461,27.9,366,20.9,256,12.2,256,12.2
25120 DATA 2825,72,1617,69,1290,66,997,60,824,51,752,39.3
25130 DATA 565,31.6,356,24.6,249,18.5,124,10.8,124,10.8
25140 DATA 2867,72,1658,69,1331,66,1038,60,824,51,806,39.3
25150 DATA 618,31.6,409,24.6,302,18.5,177,10.8,177,10.8
25160 DATA 2908,72,1699,69,1372,66,1079,60,851,51,824,41
25170 DATA 672,31.6,463,24.6,355,18.5,231,10.8,231,10.8
25180 DATA 2891,72,1683,69,1355,66,1064,60,834,51,824,41
25190 DATA 650,31.6,441,24.6,334,18.5,209,10.8,209,10.8
25200 DATA 2729,72,1521,69,1193,66,900,60,728,51,664,44.4
25210 DATA 499,35.7,314,27.9,220,20.9,110,12.2,110,12.2
25220 DATA 2770,72,1562,69,1234,66,942,60,728,51,711,44.4
25230 DATA 546,35.7,362,27.9,267,20.9,157,12.2,157,12.2
25240 DATA 2811,72,1603,69,1275,66,983,60,754,51,728,41
25250 DATA 593,35.7,409,27.9,314,20.9,204,12.2,204,12.2
25260 DATA 2795,72,1586,69,1259,66,966,60,738,51,728,41
25270 DATA 574,35.7,390,27.9,295,20.9,185,12.2,185,12.2
25280 DATA 2552,72,1343,69,1016,66,723,60,495,51,351,41
25290 DATA 190,32,108,24,108,24,108,24,108,24
25300 DATA 2636,72,1428,69,1100,66,808,60,580,51,435,41
25310 DATA 275,32,192,24,96,14,96,14,96,14
25320 DATA 2677,72,1469,69,1141,66,849,60,621,51,476,41
25330 DATA 316,32,233,24,137,14,137,14,137,14
25340 DATA 2718,72,1510,69,1183,66,890,60,662,51,517,41
25350 DATA 357,32,274,24,178,14,178,14,178,14
25360 DATA 2702,72,1493,69,1166,66,873,60,645,51,501,41
25370 DATA 344,32,258,24,162,14,162,14,162,14
25500 DATA 229625,72,133852,120861,69,58805,91401,66,39362
25510 DATA 65052,60,23553,44524,51,13084,31541,41,7761
25520 DATA 17075,32,3132,9652,24,1351,0,14,0
30000 PRINT"LOONBEREKENINGEN 1988":PRINT
30010 PRINT"Ontwikkeld op SHARP-MZ 700/800"
30020 PRINT"Auteur/maker: W.G. van SCHIP,"
30030 PRINT"Prins Willem-Alexanderweg 6,"
30040 PRINT"3991 CB  HOUTEN. tel. 03403-71634"
30050 PRINT"30 december 1987.":PRINT
30060 PRINT"Herzien op 20 januari 1988."
30100 GOSUB 7750:GOSUB 100
30110 PRINT"Iedereen mag zonder toestemming ten"
30120 PRINT"eigen behoeve gebruik maken van dit"
30130 PRINT"programma. Commercieel gebruik is"
30150 PRINT"slechts toegestaan NA uitdrukkelijke"
30160 PRINT"goedkeuring door de maker."
30170 PRINT"Hoewel getracht is dit programma geheel"
30180 PRINT"foutloos samen te stellen is en blijft"
30190 PRINT"toepassing volledig voor eigen risico."
30200 PRINT"Door de auteur wordt geen enkele aan-"
30210 PRINT"sprakelijkheid aanvaard."
30220 PRINT"Copyright"
30330 GOSUB 7750:GOSUB 100:IF MN=4 THEN 950
30340 RETURN
32000 REM TROS-RADIO dd 880203
32010 REM incl corr. dd 880206
