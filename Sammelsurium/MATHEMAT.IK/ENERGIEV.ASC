1000 A=200:GOTO 20:REM  ENERGIEVERBRUIK
1010 DIM A(12)
1020 VM=VE
1030 GOSUB 100:T=0:MO=12:XX=0:SO=0:Q=0
1040 HO=2:VE=0:GOSUB 110
1050 SR$="==============================":GOSUB 150
1060 VE=1:GOSUB 110
1070 SR$="E N E R G I E  V E R B R U I K":GOSUB 150
1080 VE=2:GOSUB 110
1090 SR$="==============================":GOSUB 150
1100 RESTORE :READ A$,B$,C$
1110 B=VAL(B$):C=VAL(C$)
1120 HO=3:VE=4:GOSUB 110
1130 PRINT A$
1140 HO=25:GOSUB 110
1150 PRINT B;"-";C
1160 PRINT:PRINT"  Men begint met het jaar in te vullen."
1170 PRINT"          ";:SR$="( DEMO 1985 )":GOSUB 150
1180 PRINT
1190 PRINT:PRINT"Het eerst komt ELECTRA aan de beurt, en"
1200 PRINT"  vervolgens GAS en WATER, door op de"
1210 PRINT"        spatiebalk te drukken."
1220 PRINT:PRINT:PRINT"      Vul 0000 in om te stoppen."
1230 PRINT:PRINT"     ";:SR$="Druk op ? voor uitleg."
1240 GOSUB 150
2000 REM  START INVOEREN.
2010 JA=0:FO=0:JA$=""
2020 HO=30:VE=22:GOSUB 110:PRINT"...."
2030 HO=5:VE=22:GOSUB 110
2040 PRINT"Welk jaar wilt U zien :  ";
2050 REM  SCHOONWISSEN TOETSENBORD BUFFER
2060 GOSUB 200:IF IN>0 THEN 2060
2070 GOSUB 210:J1$=IN$:PRINT J1$;
2080 IF IN=13 THEN 2010
2090 IF J1$="?" THEN 6000
2100 IF J1$="0" THEN 2120
2110 J1=VAL(J1$):IF J1=0 THEN 2010
2120 GOSUB 210:J2$=IN$:PRINT J2$;
2130 IF IN=13 THEN 2010
2140 IF J2$="0" THEN 2160
2150 J2=VAL(J2$):IF J2=0 THEN 2010
2160 GOSUB 210:J3$=IN$:PRINT J3$;
2170 IF IN=13 THEN 2010
2180 IF J3$="0" THEN 2200
2190 J3=VAL(J3$):IF J3=0 THEN 2010
2200 GOSUB 210:GOSUB 250:J4$=IN$:PRINT J4$;
2210 IF IN=13 THEN 2010
2220 IF J4$="0" THEN 2240
2230 J4=VAL(J4$):IF J4=0 THEN 2010
2240 JA$=J1$+J2$+J3$+J4$
2250 JA=VAL(JA$)
2260 DE$="I==I"
2270 IF JA=1985 THEN DE$="DEMO"
2280 IF JA$<>"0000" THEN 3000
2290 GOSUB 100:HO=9:VE=11:GOSUB 110
2300 SR$="EINDE PROGRAMMA":GOSUB 150
2310 HO=7:VE=VM:GOSUB 110
2320 PRINT"Druk een toets voor stop. ";
2330 GOSUB 210:GOTO 950
3000 IF (JA<B-1) OR (JA>C) THEN FO=1
3010 IF FO<>1 THEN 3090
3020 HO=5:VE=22:GOSUB 110
3030 PRINT"  ";:SR$="Verkeerde opgave !"
3040 GOSUB 150;:PRINT"    ";
3050 SD=25:GOSUB 450:T=0
3060 GOSUB 110
3070 PRINT"                              "
3080 GOTO 2010
3090 HO=5:VE=22:GOSUB 110
3100 PRINT"  ";:SR$="Even geduld a.u.b."
3110 GOSUB 150:PRINT"     "
3120 GOTO 5000
4000 SO=A(1)+A(2)+A(3)+A(4)+A(5)+A(6)+A(7)
4010 SO=SO+A(8)+A(9)+A(10)+A(11)+A(12)
4020 T=T+1
4030 B1$="Kwh.":B2$="M3.":B3$="ELECTRA"
4040 B4$="    GAS.":B5$="  WATER."
4050 IF T=1 THEN A1$=B1$:A2$=B3$
4060 IF T=2 THEN A1$=B2$:A2$=B4$
4070 IF T=3 THEN A1$=B2$:A2$=B5$
4080 FOR I=1 TO 12
4090 IF I=1 THEN A=A(I)
4100 IF A(I)>A THEN A=A(I)
4110 NEXT I
4120 A=ABS(28/A)
4130 GOSUB 100
4140 PRINT"ENERGIEVERBRUIK"
4150 HO=17:VE=0:GOSUB 110:SR=JA:GOSUB 300:GOSUB 150
4160 HO=31:GOSUB 110:PRINT A2$
4170 BB=SO/MO:PRINT"Tot: "
4180 HO=4:SR=SO:CT=8:CN=1:GOSUB 310:VE=1:GOSUB 110
4190 PRINT SR$
4200 HO=14:VE=1:GOSUB 110:PRINT A1$
4210 HO=20:GOSUB 110:PRINT DE$
4220 HO=25:CT=6:CN=1:SR=BB:GOSUB 310
4230 VE=1:GOSUB 110:PRINT SR$
4240 HO=33:GOSUB 110:PRINT"/ mnd."
4250 HO=0:VE=3:GOSUB 110:PRINT"Januari"
4260 AX=A(1):HO=10:GOSUB 5530
4270 HO=20:VE=3:GOSUB 110:PRINT"Juli"
4280 AX=A(7):HO=31:GOSUB 5530
4290 HO=0:VE=4:GOSUB 110:PRINT"Februari"
4300 AX=A(2):HO=10:GOSUB 5530
4310 HO=20:VE=4:GOSUB 110:PRINT"Augustus"
4320 AX=A(8):HO=31:GOSUB 5530
4330 HO=0:VE=5:GOSUB 110:PRINT"Maart"
4340 AX=A(3):HO=10:GOSUB 5530
4350 HO=20:VE=5:GOSUB 110:PRINT"September"
4360 AX=A(9):HO=31:GOSUB 5530
4370 HO=0:VE=6:GOSUB 110:PRINT"April"
4380 AX=A(4):HO=10:GOSUB 5530
4390 HO=20:VE=6:GOSUB 110:PRINT"Oktober"
4400 AX=A(10):HO=31:GOSUB 5530
4410 HO=0:VE=7:GOSUB 110:PRINT"Mei"
4420 AX=A(5):HO=10:GOSUB 5530
4430 HO=20:VE=7:GOSUB 110:PRINT"November"
4440 AX=A(11):HO=31:GOSUB 5530
4450 HO=0:VE=8:GOSUB 110:PRINT"Juni"
4460 AX=A(6):HO=10:GOSUB 5530
4470 HO=20:VE=8:GOSUB 110:PRINT"December"
4480 AX=A(12):HO=31:GOSUB 5530
4490 HO=0:VE=10:GOSUB 110
4500 MN$="JanFebMaaAprMeiJunJulAugSepOktNovDec"
4510 FOR I=1 TO 12
4520 PRINT MID$(MN$,3*I-2,3);":";
4530 SR$="":ST$="*":FOR J=1 TO A(I)*A
4540 SR$=SR$+ST$
4550 NEXT J
4560 Q=LEN(SR$):SX$="":SP$=" "
4570 FOR QQ=1 TO (29-Q):SX$=SX$+SP$:NEXT QQ
4580 SR$=SR$+SX$
4590 GOSUB 150
4600 PRINT
4610 NEXT I
4620 HO=0:VE=VM:GOSUB 110
4630 SR$="Druk op spatiebalk voor vervolg:"
4640 GOSUB 150
4650 GOSUB 210:IF IN$<>" " THEN GOSUB 250:GOTO 4650
4660 GOSUB 100:MO=12:RETURN
5000 RESTORE:READ A$,B$,C$
5010 READ A
5020 HO=16:VE=20:GOSUB 110:XX=XX+1:PRINT XX
5030 IF A<JA*10 THEN 5010
5040 Q=Q+1:FOR I=1 TO 12:READ A(I)
5050 IF A(I)>9999.9 THEN GOSUB 5560
5060 IF A(I)=0 THEN MO=MO-1
5070 NEXT I
5080 IF A(1)<>.01 THEN 5500
5090 HO=6:VE=20:GOSUB 110
5100 PRINT"  Geen DATA voorhanden ."
5110 PRINT
5120 IF T=0 THEN PRINT"       ";:SR$="***  Electra  ***"
5130 IF T=0 THEN GOSUB 150
5140 IF T=1 THEN PRINT"       ";:SR$="***    Gas    ***"
5150 IF T=1 THEN GOSUB 150
5160 IF T=2 THEN PRINT"       ";:SR$="***   Water   ***"
5170 IF T=2 THEN GOSUB 150
5180 SD=10:GOSUB 450
5190 MO=12
5200 T=T+1:IF T=3 THEN 1030
5210 GOTO 5040
5500 GOSUB 4000
5510 IF T=3 THEN 1030
5520 GOTO 5040
5530 CT=7:CN=1:SR=AX:GOSUB 310
5540 GOSUB 110:PRINT SR$
5550 RETURN
5560 GOSUB 100
5570 HO=0:VE=2:GOSUB 110
5580 PRINT"    ******";JA;"*** ";
5590 IF Q=1 THEN PRINT"ELECTRA ******"
5600 IF Q=2 THEN PRINT"GAS ******"
5610 IF Q=3 THEN PRINT"WATER ******"
5620 PRINT:PRINT:PRINT
5630 PRINT"U heeft DATA ingevoerd groter dan"
5640 PRINT:PRINT"de toegestane waarde van 9999.9"
5650 PRINT:PRINT"per maand, verander dit, of pas het"
5660 PRINT:PRINT"programma aan in 10.000 t/m 13.070."
5670 PRINT:PRINT"Dit omdat anders de getallen niet meer"
5680 PRINT:PRINT"netjes onder elkaar afgedrukt worden."
5690 HO=0:VE=VM:GOSUB 110
5700 PRINT"Druk op spatiebalk voor verder : ";
5710 GOSUB 210
5720 IF IN$=" " THEN RETURN
5730 GOTO 5690
6000 GOSUB 100
6010 PRINT"Met dit programma kunt U uw energie"
6020 PRINT"verbruik per maand zichtbaar maken."
6030 PRINT:PRINT"Links boven wordt het totaal bijge-"
6040 PRINT"houden, en rechts boven het gemiddelde"
6050 PRINT"verbruik per maand."
6060 PRINT:PRINT"In DATA regel 25000 kunt U de tekst:"
6070 PRINT"DATA v.a. 25000 veranderen in een eigen"
6080 PRINT"tekst, b.v. Uw naam of straat + nr."
6090 PRINT:PRINT"De tekst niet langer maken dan de"
6100 PRINT"bestaande tekst."
6110 PRINT:PRINT"De gegevens 1986 / 2000 niet veranderen."
6120 HO=0:VE=VM:GOSUB 110
6130 SR$="Druk op spatiebalk voor verder."
6140 GOSUB 150
6150 GOSUB 210:IF IN$=" " THEN 7000
6160 GOTO 6120
7000 GOSUB 100
7010 PRINT"Opbouw DATA regels."
7020 PRINT"==================="
7030 PRINT:PRINT"Het programma zoekt eerst naar het"
7040 PRINT"gegeven jaar*10."
7050 PRINT:PRINT"Dan zal het programma de eerste"
7060 PRINT"12 gegevens uitlezen en verwerken."
7070 PRINT:PRINT"De eerste reeks is ELECTRA."
7080 PRINT"De tweede reeks is GAS."
7090 PRINT"De derde reeks is dan voor het WATER."
7100 PRINT:PRINT"De . in de DATA regels is een decimale"
7110 PRINT"komma let hier dus goed op bij het"
7120 PRINT"invoeren."
7130 PRINT:PRINT"Het getal 0.01 geeft aan voor het"
7140 PRINT"programma, dat er geen DATA beschikbaar"
7150 PRINT"is, dit getal veranderd U gewoon in de"
7160 PRINT"gewenste waarde als U de DATA regels in"
7170 PRINT"gaat vullen."
7180 HO=0:VE=VM:GOSUB 110
7190 SR$="Druk op spatiebalk voor verder."
7200 GOSUB 150
7210 GOSUB 210:IF IN$=" " THEN 7230
7220 GOTO 7180
7230 GOSUB 100
7240 PRINT"Het grootste getal dat U mag invoeren"
7250 PRINT"is 9999.9 , en het kleinste is 0.1"
7290 PRINT
7300 PRINT:PRINT:PRINT"Veel plezier met dit programma."
7310 HO=0:VE=VM:GOSUB 110
7320 SR$="Druk op spatiebalk voor start."
7330 GOSUB 150
7340 GOSUB 210:IF IN$=" " THEN 1030
7350 GOTO 7310
25000 REM  DE GETALLEN 1986 EN 2000 NIET VERANDEREN.
25010 DATA "DATA v.a. 25000","1986","2000"
25020 REM  data demo reeks voor 1985.
25030 DATA 19850
25040 DATA .2,.4,.6,.8,1,1.2,1.2,1,.8,.6,.4,.2
25050 DATA 90,70,50,30,10,3,3,10,30,50,70,90
25060 DATA 6,5,7,6,7,5,8,8,5,6,5,7
25070 REM 
25080 REM  data Electra/Gas/Water 1986.
25090 DATA 19860
25100 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25110 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25120 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25130 REM 
25140 REM  data Electra/Gas/Water 1987.
25150 DATA 19870
25160 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25170 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25180 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25190 REM 
25200 REM 
25210 REM  data Electra/Gas/Water 2000.
25220 DATA 20000
25230 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25240 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25250 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
32000 REM  *******************
32010 REM  ** AANGEPAST AAN **
32020 REM  ** BASICODE  III **
32030 REM  **  H. v.  Leen  **
32040 REM  **   HEEMSKERK   **
32050 REM  *******************
32060 REM  **   PROGRAMMA   **
32070 REM  **    ENERGIE    **
32080 REM  **  GESCHREVEN   **
32090 REM  **  OP     EEN   **
32100 REM  **  P2000T/102   **
32110 REM  *******************
32120 REM  **  21 12  1986  **
32130 REM  *******************
32200 REM TROS-radio dd 870204
