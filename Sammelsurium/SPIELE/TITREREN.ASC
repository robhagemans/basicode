1000 a=1000:GOTO 20:REM  titreren
1010 GOSUB 2460:REM  init
1020 GOSUB 2040
1030 REM --titel
1040 d$="*3":GOSUB 2000
1050 w=0:ho=21:FOR ve=2 TO 6:GOSUB 2380:NEXT ve
1060 FOR ve=8 TO 10 STEP 2:GOSUB 2380:NEXT ve
1070 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOSUB 2080
1080 GOSUB 2040:d$="*4":GOSUB 2000
1090 ho=14:FOR ve=2 TO 6 STEP 2:GOSUB 2360:NEXT ve
1100 GOSUB 210:IF (in$="n") OR (in$="N") THEN 1420
1110 GOSUB 2040:d$="*5":GOSUB 2000
1120 ho=16:FOR ve=2 TO 10 STEP 2:GOSUB 2360:NEXT ve
1130 FOR ve=14 TO 18 STEP 2:ho=16:GOSUB 2360
1140 ho=22:GOSUB 2360:NEXT ve
1150 ve=21:ho=15:GOSUB 2360
1160 GOSUB 1980:IF in$="" THEN 1180
1170 a=ASC(in$)
1180 IF a=32 THEN 1220:REM -klaar
1190 IF (a=83) OR (a=115) THEN GOSUB 1590:REM -straal
1200 IF (a=68) OR (a=100) THEN GOSUB 1660:REM -druppel
1210 GOTO 1160
1220 REM -klaar
1230 d$="*7":GOSUB 2000:GOSUB 100
1240 IF x=7 THEN s=INT((fd-sd+1)/(fd+sd-1)*100)
1250 w=0:ve=2:ho=2:GOSUB 2380:ve=4
1260 w=1:GOSUB 2380:READ a$
1270 IF x<>7 THEN ho=27:GOSUB 2340:GOTO 1290
1280 PRINT" ";ml/10;" n "
1290 w=0.2:ve=6:ho=2:GOSUB 2380
1300 PRINT" ";INT(t/20);:ho=14:GOSUB 2360
1310 w=2:ve=8:ho=2:GOSUB 2380:PRINT s;"%"
1320 w=0.5:ve=10:GOSUB 2380
1330 IF x<7 THEN v=0:GOTO 1390
1340 IF x=8 THEN v=1:GOTO 1390
1350 IF s<95 THEN v=2
1360 IF s<85 THEN v=3
1370 IF s<60 THEN v=4:GOTO 1390
1380 v=5
1390 GOSUB 260:ve=12:a$=r$(5*v+5*rv+1):GOSUB 2340
1400 w=2:ve=14:GOSUB 2380
1410 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOSUB 2470:GOTO 1020
1420 GOSUB 100:ve=10:ho=0:GOSUB 2380
1430 end
1440 REM -kraan-open
1450 ho=4:a$(1)="!=!  o":a$(2)="(i)==x":a$(3)=" H   o"
1460 FOR i=1 TO 3:ve=14+i:a$=a$(i)
1470 GOSUB 2340:NEXT:RETURN
1480 REM -kraan-dicht
1490 ho=4:a$(1)="!=!   "
1500 a$(2)="(-)==o":a$(3)=" H     "
1510 FOR i=1 TO 3:ve=14+i:a$=a$(i):GOSUB 2340:NEXT
1520 ho=5:FOR ve=19 TO 20:a$=" "
1530 GOSUB 2340:NEXT ve:RETURN
1540 REM -plons
1550 w=0.002:ho=5:ve=21
1560 FOR i=1 TO 3:GOSUB 2450:GOSUB 110
1570 PRINT p$(i):NEXT i
1580 RETURN
1590 REM -straaltje
1600 GOSUB 1440:dl=0
1610 ho=5:FOR ve=19 TO 21:a$="!":GOSUB 2340:NEXT ve
1620 fd=fd+1:GOSUB 1540:GOSUB 1720
1630 GOSUB 1980:IF in$="" THEN 1620
1640 a=ASC(in$):IF (a=83) OR (a=115) THEN 1620
1650 GOSUB 1480:RETURN
1660 REM -druppels
1670 GOSUB 1440:dl=0.1
1680 sd=sd+1:GOSUB 1480
1690 ho=5:FOR ve=19 TO 21:a$=".":GOSUB 2340
1700 a$=" ":GOSUB 2340:NEXT ve
1710 GOSUB 1540:GOSUB 1720:a=0:RETURN
1720 REM -bepaal-reaktie
1730 m=m+1:IF m=8 THEN n=n+1:m=0
1740 IF ss=0 THEN ss=1:ts=t1
1750 IF n>15 THEN GOSUB 1480:RETURN
1760 ho=5
1770 IF m=0 THEN ve=n-1:a$=" ":GOSUB 2340:GOTO 1790
1780 ve=n:a$=MID$(l$,m,1):GOSUB 2340
1790 dd=ep-fd-sd
1800 ml=INT((fd+sd)/.16)/100:ve=21:ho=9
1810 GOSUB 110:PRINT ml
1820 x=8:IF dd=0 THEN x=7
1830 IF dd>0 THEN x=6
1840 IF dd>1 THEN x=5
1850 IF dd>3 THEN x=4
1860 IF dd>5 THEN x=3
1870 IF dd>8 THEN x=2
1880 IF dd>11 THEN x=1
1890 IF dd>16 THEN x=0
1900 IF dd>25 THEN 1910
1910 ho=4:q=0:i=x*4+1:FOR e=i TO i+3:ve=21
1920 IF q=1 THEN ve=22
1930 IF q=2 THEN ve=22
1940 GOSUB 110:PRINT e$(e);
1950 q=q+1
1960 w=dl:GOSUB 2450:NEXT e
1970 RETURN
1980 REM -toets+tijd
1990 GOSUB 200:t=t+1:RETURN
2000 REM -zoek-data
2010 RESTORE
2020 READ a$:IF a$=d$ THEN RETURN
2030 GOTO 2020
2040 REM --teken buret
2050 GOSUB 100:d$="*2":GOSUB 2000
2060 ho=0:FOR ve=0 TO 23:GOSUB 2360:NEXT
2070 RETURN
2080 REM --uitleg
2090 d$="*6":GOSUB 2000
2100 GOSUB 100:ho=0:w=0
2110 FOR ve=1 TO 3 STEP 2:GOSUB 2380:NEXT ve
2120 ve=12:w=2:GOSUB 2380
2130 w=0:ho=4
2140 FOR ve=6 TO 10:GOSUB 2380:NEXT ve
2150 w=2:FOR i=1 TO 2:ve=12
2160 ho=0:GOSUB 2380:ve=10-i:ho=5:GOSUB 2380:NEXT i
2170 w=3:ve=12:ho=0:GOSUB 2380:ho=5:READ a$(1),a$(2)
2180 FOR i=1 TO 100:ve=8:IF i/2=INT(i/2) THEN ve=9
2190 GOSUB 260:a=1+INT(rv+0.5)
2200 a$=a$(a):GOSUB 2340:NEXT i
2210 w=3:ho=0:ve=14:GOSUB 2380
2220 w=4:ho=13:ve=7:GOSUB 2380:ve=9
2230 GOSUB 2380:GOSUB 2400
2240 w=4:ho=0:ve=12:GOSUB 2380:FOR i=1 TO 3
2250 GOSUB 2400:ho=0
2260 FOR ve=14 TO 16 STEP 2:GOSUB 2380:NEXT ve
2270 ho=5:READ a$
2280 FOR ve=8 TO 9:GOSUB 2340:NEXT ve:NEXT i
2290 ho=0:GOSUB 2400
2300 FOR ve=14 TO 16 STEP 2:GOSUB 2380:NEXT ve
2310 w=5:GOSUB 2450:GOSUB 100
2320 ve=10:GOSUB 2360:GOSUB 2450
2330 RETURN
2340 REM PRINT a$;
2350 GOSUB 110:PRINT a$;:RETURN
2360 REM -READ a$+PRINT 
2370 READ a$:GOSUB 110:PRINT a$;:RETURN
2380 REM -wacht
2390 GOSUB 2450:READ a$:GOSUB 110:PRINT a$;:RETURN
2400 REM -regels-wissen
2410 GOSUB 2450
2420 ho=0:FOR ve=12 TO 19:GOSUB 110
2430 PRINT"                                      "
2440 NEXT ve:RETURN
2450 FOR w1=0 TO w*800:NEXT w1:RETURN
2460 DIM a$(3),e$(36),r$(30),p$(3)
2470 RESTORE:READ l$
2480 FOR i=1 TO 3:READ p$(i):NEXT i
2490 FOR i=1 TO 36:READ e$(i):NEXT i
2500 FOR i=1 TO 30:READ r$(i):NEXT i
2510 GOSUB 260:ss=0:m=4:n=1:fd=0:s=0:t1=0:a=0
2520 sd=0:ep=INT(115*rv)
2530 RETURN
25000 DATA "====----","x","+","0"
25010 DATA "o+o","ooo","ooo","ooo"
25020 DATA "o+o","o*o","ooo","ooo"
25030 DATA "+*+","o+o","ooo","ooo"
25040 DATA "+*+","+*+","ooo","ooo"
25050 DATA "***","**+","ooo","ooo"
25060 DATA "***","***","ooo","ooo"
25070 DATA "***","***","ooo","o0o"
25080 DATA "***","***","+++","+++"
25090 DATA "***","***","***","***"
25100 DATA "niet op eindpunt!","is dat alles? ","te vlug !"
25110 DATA "nog daar?","zoek wat anders !","linkshandig?"
25120 DATA "ben je dom?","eindpunt voorbij","die is er aan!"
25130 DATA "niet trillen","snzz! te zacht!","geen hele dag!"
25140 DATA "vakbondsman?","kies ander vak!","drup! drup!"
25150 DATA "beter nog eens!","oefenen!","niet gek voor jou"
25160 DATA "zag slechtere","voel je je goed?","dat is het !"
25170 DATA "ziet er goed uit!","kon vlugger","bijna perfect"
25180 DATA "wordt beter","ben jij 't garry?","zoek je werk?"
25190 DATA "perfect gedaan","fantastisch","kon niet beter!"
25500 DATA "*2","I   ! !"
25510 DATA "I   !=!-0"
25520 DATA "I   !=!-"
25530 DATA "I   !=!-1"
25540 DATA "I   !=!-"
25550 DATA "I   !=!-2"
25560 DATA "I===‚=-"
25570 DATA "I   !=!-3"
25580 DATA "I   !=!-"
25590 DATA "I   !=!-4"
25600 DATA "I   !=!-"
25610 DATA "I   !=!-5"
25620 DATA "I   !=!-"
25630 DATA "I   !=!-6"
25640 DATA "I   !=!-"
25650 DATA "I   !=!"
25660 DATA "I   (-)==o"
25670 DATA "I    H"
25680 DATA "I    V"
25690 DATA "I"
25700 DATA "I  !   !"
25710 DATA "I  !ooo!"
25720 DATA "I  !ooo!"
25730 DATA "================"
25800 DATA "*3","             "
25810 DATA " titratie-   "
25820 DATA "             "
25830 DATA " simulator   "
25840 DATA "             "
25850 DATA "g.j.flynn"
25860 DATA "wil je uitleg"
25870 DATA "*4","kijk..ik heb de "
25880 DATA "oplossingen klaargezet"
25890 DATA "eens proberen"
25900 DATA "ok luiwammes"
25910 DATA "*5","-----------------"
25920 DATA "computer titrator"
25930 DATA " buret bedienen  "
25940 DATA "    druk op :    "
25950 DATA "-----------------"
25960 DATA "s","-voor stromen"
25970 DATA "d","-voor druppels"
25980 DATA "spatie","-voor klaar"
25990 DATA "ml 1.000 n HCl"
26000 DATA "*6","we moeten de sterkte bepalen van een   "
26010 DATA "    basische (NaOH) oplossing          "
26020 DATA "we nemen een bekerglas"
26030 DATA "!     !"
26040 DATA "!     !"
26050 DATA "!     !"
26060 DATA "!     !"
26070 DATA " ----- "
26100 DATA "en doen er 10 ml base (NaOH) in. "
26110 DATA "ooooo"
26120 DATA "dan doen we er wat zuur (HCl) bij"
26130 DATA "00000"
26140 DATA "nu reageert het zuur met de base "
26150 DATA "0o0o0"
26160 DATA "o0o0o"
26180 DATA "en er onstaat zout (NaCl) + water"
26190 DATA "NaOH + HCl--> NaCl + HOH"
26200 DATA "base  zuur    zout  water"
26210 DATA "we doen een indicator bij de oplossing "
26220 DATA "dan precies genoeg zuur om met de base "
26230 DATA "te reageren en zie..de kleur verandert "
26240 DATA "+++++"
26250 DATA "nemen we te veel zuur,dan gaan we "
26260 DATA "over het 'eindpunt' van de reaktie"
26270 DATA "*****"
26280 DATA "we bepalen hoeveel zuur"
26290 DATA "nodig is om het eindpunt te bereiken"
26300 DATA "+++++"
26310 DATA "we kennen de sterkte van het zuur en..."
26320 DATA "kunnen de sterkte van de base berekenen"
26330 DATA "het is niet zo moeilijk als het lijkt"
26500 DATA "*7","analyse is klaar"
26510 DATA "het monster is","..verpest"
26520 DATA "je had","seconden nodig"
26530 DATA "je klasse "
26540 DATA "---------------"
26550 DATA "nog eens"
26560 DATA "jammer,ik dacht dat je 't goed wou leren"
30000 REM 
30010 REM           titreren
30020 REM   een onderwijskundig programma
30030 REM         door g.j. flynn
30050 REM   bewerkt door rien van dongen
30060 REM   en leden van de basicodegroep
