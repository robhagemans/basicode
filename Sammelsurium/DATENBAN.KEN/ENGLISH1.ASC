1000 A=6000:GOSUB 20:REM ENGLISH
1010 GOSUB 100:S=HO:Z=VE:HO=4:VE=2:GOSUB 110
1020 SR$="BIDAT":GOSUB 150
1030 PRINT"   PRESENTS":PRINT
1040 PRINT"**** *   *  **** *    ***  **** *   *"
1050 PRINT"*    **  * *     *     *  *     *   *"
1060 PRINT"***  * * * *  ** *     *   ***  *****"
1070 PRINT"*    *  ** *   * *     *      * *   *"
1080 PRINT"**** *   *  ***  **** *** ****  *   *"
1090 HO=4:VE=10:GOSUB 110
1100 SR$="FOR COMPUTER FREAKS":GOSUB 150
1110 DIM V$(300,1),V(300):RESTORE:N=0
1120 READ V$(N,0),V$(N,1):V(N)=0
1130 IF V$(N,0)<>"*" THEN N=N+1:GOTO 1120
1140 N=N-1
1150 PRINT:PRINT:SR$="BETRIEBSARTEN":GOSUB 150
1160 PRINT:PRINT:PRINT"  <1>=EINFUEHRUNG"
1170 PRINT"  <2>=LEKTIONSAUSWAHL"
1180 PRINT"  <3>=VORSTELLUNG DER VOKABELN"
1190 PRINT"  <4>=UEBUNG ENGLISCH-DEUTSCH"
1200 PRINT"  <5>=UEBUNG DEUTSCH-ENGLISCH"
1210 PRINT"  <6>=DRUCK DER VOKABELLISTE"
1220 PRINT"  <0>=ENDE"
1230 PRINT:SR$="IHRE AUSWAHL":GOSUB 150
1240 INPUT M:IF M=0 THEN 1570
1250 ON M GOSUB 2180,1270,1320,1420,1460,1510
1260 GOSUB 100:GOTO 1150
1270 PRINT:PRINT:SR$="WELCHE LEKTION WUENSCHEN SIE ?"
1280 GOSUB 150:PRINT:PRINT:INPUT"   (1-14) ";L
1290 IF (L<1) OR (L>14) THEN 1280
1300 LA=1+(L-1)*20:LE=20*L:UD=0:UE=0
1310 RETURN
1320 GOSUB 100:PRINT"IN DER ";L;". LEKTION LERNEN SIE"
1330 PRINT"FOLGENDE VOKABELN:(WEITER MIT <ENTER>)"
1340 PRINT"ENGLISCH";TAB(S/2);"DEUTSCH"
1350 FOR I=1 TO S:PRINT"=";:NEXT I:PRINT
1360 FOR I=LA TO LE:LW=LEN(V$(I,0)):LV=LEN(V$(I,1))
1370 PRINT V$(I,0);:IF (LW>=S/2) OR (LV>=S/2) THEN PRINT
1380 LU=S/2:IF (LW>=S/2) OR (LV>S/2) THEN LU=S-LV
1390 PRINT TAB(LU);V$(I,1):GOSUB 210
1400 IF IN$="$" THEN I=LE
1410 NEXT I:RETURN
1420 UD=UD+1:I1=0:I2=1:L1=LA:L2=LE:L0=30
1430 UU=UD:IF UD>1 THEN L1=1:L0=40
1440 GOSUB 1590:IF SR$="J" THEN UD=UD-1:GOTO 1420
1450 IF SR$="N" THEN RETURN
1460 UE=UE+1:I1=1:I2=0:L1=LA:L2=LE:L0=30
1470 UU=UE:IF UE>1 THEN L1=1:L0=40
1480 GOSUB 1590:IF SR$="J" THEN UE=UE-1:GOTO 1420
1490 IF SR$="N" THEN RETURN
1500 GOTO 1420
1510 GOSUB 100:SR$="AUSDRUCK DES VOKABULARS":GOSUB 150
1520 PRINT:PRINT:PRINT"  SORTIERMODUS"
1530 PRINT:PRINT" <0>=UNSORTIERT":PRINT" <1>=ENGLISCH"
1540 PRINT" <2>=DEUTSCH":PRINT:INPUT"IHRE AUSWAHL ? ";L0
1550 IF (L0<0) OR (L0>2) THEN RETURN
1560 GOTO 1950
1570 SR$="ENDE":PRINT:PRINT:GOSUB 150
1580 SD=99:GOSUB 450:GOSUB 950
1590 FOR I=L1 TO L2:V(I)=0:NEXT I
1600 L3=1+L2-L1:J=0:FOR I=1 TO L0:GOSUB 100
1610 PRINT L;". LEKTION ";UU;". UEBUNG"
1620 PRINT:SR$=V$(0,I1)+"  -  "+V$(0,I2):GOSUB 150:PRINT
1630 PRINT:PRINT"BISHER ";J;" FEHLER IN ";I-1;" FRAGEN"
1640 GOSUB 260:K=L1+INT(L3*RV):IF V(K)>1 THEN 1640
1650 V(K)=V(K)+1
1660 PRINT:PRINT V$(0,I1);" : ";V$(K,I1)
1670 PRINT:PRINT V$(0,I2);:INPUT" ? ";SR$:PRINT
1680 GOSUB 330:A$=SR$
1690 IF A$=V$(K,I2) THEN SR$="RICHTIG":GOSUB 150:GOTO 1790
1700 GOSUB 250:INPUT"WOLLEN SIE KORRIGIEREN J/N ?";SR$
1710 GOSUB 330:IF SR$="J" THEN 1670
1720 Q=0:FOR QO=1 TO N
1730 IF (V$(QO,I2)=A$) AND (V$(QO,I1)=V$(K,I1)) THEN Q=1:QO=N
1740 NEXT QO
1750 IF Q>0 THEN PRINT"GLUECK GEHABT ! RICHTIG !":GOTO 1790
1760 PRINT"LEIDER NICHT RICHTIG !":J=J+1:V(K)=V(K)-1
1770 PRINT"DIE RICHTIGE ANTWORT WAERE"
1780 PRINT:SR$=V$(K,I2):GOSUB 150:SD=20:GOSUB 450
1790 SD=15:GOSUB 450:NEXT I:A$=""
1800 PRINT:PRINT"IN DIESER UEBUNG WURDEN VON IHNEN"
1810 PRINT"IN ";L0;" FRAGEN ";J;" FEHLER GEMACHT.":PRINT
1820 IF J/I<.06 THEN SR$="DAS IST SPITZE":GOSUB 150:GOTO 1920
1830 IF J/I<.2 THEN SR$="DAS GING RECHT GUT":GOSUB 150:GOTO 1920
1840 IF J/I<.5 THEN SR$="NAJA,ZUR NOT GINGS":GOSUB 150:GOTO 1920
1850 PRINT"DAS SIND ZU VIELE FEHLER!"
1860 PRINT"SIE MUESSEN GENAU DIE ANTWORTEN"
1870 PRINT"BRINGEN,DIE DER COMPUTER BEI DER"
1880 PRINT"VORSTELLUNG DER VOKABELN ZEIGTE."
1890 PRINT"DIESE UEBUNG SOLLTEN SIE WIEDER-"
1900 INPUT"HOLEN. EINVERSTANDEN J/N ?";SR$
1910 GOSUB 330:IF SR$="J" THEN RETURN
1920 PRINT:PRINT:INPUT"WEITER MIT NAECHSTER UEBUNG?";SR$
1930 GOSUB 330:IF SR$="J" THEN SR$="Y"
1940 RETURN
1950 PRINT:PRINT:SR$="BITTE WARTEN - SORTIERUNG":GOSUB 150
1960 L0=L0-1:L1=1:IF L0=1 THEN L1=0
1970 IF L0<0 THEN L0=0:GOTO 2030
1980 FOR I=1 TO N-1:A$=V$(I,L0):A=I:FOR J=I+1 TO N
1990 IF V$(J,L0)<A$ THEN A$=V$(J,L0):A=J
2000 NEXT J:B$=V$(A,L1):V$(A,L0)=V$(I,L0)
2010 V$(A,L1)=V$(I,L1):V$(I,L0)=A$:V$(I,L1)=B$
2020 PRINT"*";:NEXT I
2030 PRINT:INPUT"DRUCKER BEREIT ? ";SR$
2040 GOSUB 330:IF SR$="N" THEN RETURN
2050 SR$="ENGLISH FOR COMPUTER FREAKS"
2060 GOSUB 360:GOSUB 350:GOSUB 360:A=65
2070 FOR I=1 TO N:IF A<64 THEN 2120
2080 IF A=64 THEN INPUT"NEUE SEITE <ENTER>";A$
2090 A=1:SR$=V$(0,L0)+"             "+V$(0,L1)
2100 GOSUB 350:GOSUB 360:SR$="=========="
2110 FOR K=1 TO 4:GOSUB 350:NEXT K:GOSUB 360
2120 SR$=LEFT$(V$(I,L0)+"                  ",20)
2130 GOSUB 350:SR$=V$(I,L1):GOSUB 350:GOSUB 360
2140 A=A+1:NEXT I:INPUT"NOCHMAL J/N ?";SR$
2150 GOSUB 330:IF SR$="J" THEN 2030
2160 RESTORE:FOR I=0 TO N:READ V$(I,0),V$(I,1):NEXT I
2170 RETURN
2180 GOSUB 100
2190 PRINT"IN DER  RECHENTECHNIK  WIMMELT  ES  VON"
2200 PRINT"ENGLISCHEN FACHAUSDRUECKEN. DIESES PRO-"
2210 PRINT"GRAMM SOLL  DEM  NICHT  DER  ENGLISCHEN"
2220 PRINT"SPRACHE KUNDIGEN EINEN  ZUGANG  ZU  DEN"
2230 PRINT"BEGRIFFEN  DER COMPUTEREI     SCHAFFEN."
2240 PRINT"DER ENGLISCHEXPERTE WIRD SICH  MANCHMAL"
2250 PRINT"UEBER EIGENWILLIGE UEBERSETZUNGEN  WUN-"
2260 PRINT"DERN, ABER ZWISCHEN UMGANGSENGLISCH UND"
2270 PRINT"COMPUTERGEBRAUCH    SIND    KOMPROMISSE"
2280 PRINT"NOETIG. ARBEITEN SIE DER REIHE NACH DIE"
2290 PRINT"LEKTIONEN AB. EINZELNE UEBUNGEN KOENNEN"
2300 PRINT"SIE GEGEBENENFALLS  UEBERSPRINGEN, WENN"
2310 PRINT"SIE SICHER SIND. IN SPAETEREN UEBUNGEN "
2320 PRINT"WERDEN  AUCH  VOKABELN  AUS  VORHERIGEN"
2330 PRINT"LEKTIONEN MIT ABGEFRAGT. DABEI  KOENNEN"
2340 PRINT"EINZELNE WORTE  AUCH  ZWEIMAL ABGEFRAGT"
2350 PRINT"WERDEN.  DANN VIEL ERFOLG !     <ENTER>"
2360 INPUT"";A$:RETURN
25000 DATA "ENGLISH","DEUTSCH"
25010 DATA "BEGIN","ANFANG"
25020 DATA "COMPUTER","RECHNER"
25030 DATA "COUNT","ZAEHLEN"
25040 DATA "DEVICE","GERAET"
25050 DATA "DATA","DATEN"
25060 DATA "BINARY","BINAER"
25070 DATA "DIGIT","ZIFFER"
25080 DATA "DISPLAY","ANZEIGE"
25090 DATA "OUTPUT","AUSGABE"
25100 DATA "KEYBOARD","TASTATUR"
25110 DATA "INPUT","EINGABE"
25120 DATA "NEW","NEU"
25130 DATA "MEMORY","SPEICHER"
25140 DATA "LIST","AUFLISTEN"
25150 DATA "NO","NEIN","YES","JA"
25160 DATA "THEN","DANN","IF","WENN"
25170 DATA "GO","GEHEN","TO","ZU"
25180 DATA "RUN","LAUFEN"
25190 DATA "BIT","BINAEREINHEIT"
25200 DATA "START","START","END","ENDE"
25210 DATA "COMPILER","UEBERSETZER"
25220 DATA "BYTE","DATENWORT"
25230 DATA "DIRECTORY","VERZEICHNIS"
25240 DATA "OPEN","OEFFNEN"
25250 DATA "CLOSE","SCHLIESSEN"
25260 DATA "FILE","DATEI"
25270 DATA "DELETE","LOESCHEN"
25280 DATA "CANCEL","STREICHEN"
25290 DATA "PRINT","DRUCKEN"
25300 DATA "PAGE","SEITE"
25310 DATA "PUNCH","STANZEN"
25320 DATA "READ","LESEN"
25330 DATA "RESET","RUECKSETZEN"
25340 DATA "CALL","RUFEN"
25350 DATA "DISK","PLATTE"
25360 DATA "DRIVE","LAUFWERK"
25370 DATA "TIME","ZEIT"
25380 DATA "SPACE","RAUM"
25390 DATA "WAIT","WARTEN"
25400 DATA "STOP","STOP"
25410 DATA "ENTRY","EINGANG"
25420 DATA "SET","SETZEN"
25430 DATA "SWITCH","SCHALTER"
25440 DATA "UNIT","EINHEIT"
25450 DATA "OPERATOR","BEDIENER"
25460 DATA "USER","NUTZER"
25470 DATA "TERMINAL","ENDSTELLE"
25480 DATA "MONITOR","BILDSCHIRMEINHEIT"
25490 DATA "HARDWARE","GERAETETECHNIK"
25500 DATA "SOFTWARE","PROGRAMME"
25510 DATA "QUESTION","FRAGE"
25520 DATA "ANSWER","ANTWORT"
25530 DATA "WINDOW","FENSTER"
25540 DATA "ZERO","NULL"
25550 DATA "LEFT","LINKS"
25560 DATA "RIGHT","RECHTS"
25570 DATA "LOAD","LADEN"
25580 DATA "SAVE","RETTEN"
25590 DATA "STATEMENT","ANWEISUNG"
25600 DATA "READY","BEREIT"
25610 DATA "ERASE","LOESCHEN"
25620 DATA "COMPARE","VERGLEICHEN"
25630 DATA "ACCESS","ZUGRIFF"
25640 DATA "COMPRESS","VERDICHTEN"
25650 DATA "CLOCK","TAKT"
25660 DATA "POWER","LEISTUNG"
25670 DATA "EXIT","AUSGANG"
25680 DATA "FREE","FREI"
25690 DATA "LINE","ZEILE"
25700 DATA "SIZE","GROESSE"
25710 DATA "LENGTH","LAENGE"
25720 DATA "WIDTH","WEITE"
25730 DATA "BUFFER","PUFFER"
25740 DATA "LOW","NIEDRIG"
25750 DATA "HIGH","HOCH"
25760 DATA "LEVEL","PEGEL"
25770 DATA "LIMIT","GRENZWERT"
25780 DATA "ABSOLUTE","ABSOLUT"
25790 DATA "ASSEMBLER","UEBERSETZER"
25800 DATA "DESTINATION","ZIEL"
25810 DATA "SOURCE","QUELLE"
25820 DATA "COPY","KOPIEREN"
25830 DATA "MESSAGE","NACHRICHT"
25840 DATA "JOB","AUFTRAG"
25850 DATA "SHIFT","SCHIEBEN"
25860 DATA "CIRCUIT","SCHALTKREIS"
25870 DATA "REAL","REELL"
25880 DATA "PRECISION","GENAUIGKEIT"
25890 DATA "ELSE","SONST"
25900 DATA "DIGITAL","DIGITAL"
25910 DATA "PROGRAM","PROGRAMM"
25920 DATA "PROCEDURE","PROZEDUR"
25930 DATA "OPERATING SYSTEM","BETRIEBSSYSTEM"
25940 DATA "SUBROUTINE","UNTERPROGRAMM"
25950 DATA "RETURN","ZURUECK"
25960 DATA "FUNCTION","FUNKTION"
25970 DATA "LINK","VERBINDEN"
25980 DATA "CONNECTION","VERBINDUNG"
25990 DATA "REAL TIME","ECHTZEIT"
26000 DATA "ON LINE","DIREKT AM RECHNER"
26010 DATA "COMMAND","KOMMANDO"
26020 DATA "ASSIGN","ZUWEISEN"
26030 DATA "FLOPPY DISK","DISKETTE"
26040 DATA "CLUSTER","BLOCK"
26050 DATA "FORMAT","FORMAT"
26060 DATA "INITIALIZE","INITIALISIEREN"
26070 DATA "CHECK","PRUEFEN"
26080 DATA "CODE","KODE"
26090 DATA "REPORT","BERICHT"
26100 DATA "ADDRESS","ADRESSE"
26110 DATA "VALUE","WERT","VALID","GUELTIG"
26120 DATA "INVALID","UNGUELTIG"
26130 DATA "STRING","ZEICHENKETTE"
26140 DATA "LET","LASSEN"
26150 DATA "STEP","SCHRITT"
26160 DATA "ESCAPE","UMSCHALTEN"
26170 DATA "QUICK","SCHNELL"
26180 DATA "GROUND","GRUND"
26190 DATA "FOREGROUND","VORDERGRUND"
26200 DATA "BACKGROUND","HINTERGRUND"
26210 DATA "BORDER","RAND"
26220 DATA "INK","TINTE"
26230 DATA "INCREMENT","ERHOEHUNG"
26240 DATA "DECREMENT","VERMINDERUNG"
26250 DATA "INTERPRETER","UEBERSETZER"
26260 DATA "CONTROL","STEUERUNG"
26270 DATA "ARITHMETIC","ARITHMETISCH"
26280 DATA "AUTOMATIC","AUTOMATISCH"
26290 DATA "CYCLE","ZYKLUS"
26300 DATA "RECORD","DATENSATZ"
26310 DATA "REWIND","ZURUECKSPULEN"
26320 DATA "DETECT","FINDEN"
26330 DATA "POINT","PUNKT"
26340 DATA "AT","AUF"
26350 DATA "AMOUNT","ANZAHL"
26360 DATA "CHANNEL","KANAL"
26370 DATA "CENTRAL","ZENTRAL"
26380 DATA "STORAGE","SPEICHER"
26390 DATA "EXECUTE","AUSFUEHREN"
26400 DATA "CONSOLE","BEDIENEINHEIT"
26410 DATA "HARDCOPY","PAPIERAUSDRUCK"
26420 DATA "CARD","KARTE"
26430 DATA "BELL","GLOCKE"
26440 DATA "CHARACTER","DRUCKZEICHEN"
26450 DATA "CHECKSUM","KONTROLLSUMME"
26460 DATA "ERROR","FEHLER"
26470 DATA "FLOAT","GLEITEN"
26480 DATA "FIXED","FEST"
26490 DATA "STACK","KELLERSPEICHER"
26500 DATA "PORT","TOR"
26510 DATA "INTERRUPT","UNTERBRECHUNG"
26520 DATA "CHAIN","KETTE"
26530 DATA "BREAK","UNTERBRECHEN"
26540 DATA "ACCEPT","ANNEHMEN"
26550 DATA "CARRY","UEBERTRAG"
26560 DATA "COMPLEMENT","UMKEHRUNG"
26570 DATA "CLEAR","LOESCHEN"
26580 DATA "ACCUMULATOR","AKKUMULATOR"
26590 DATA "ACCOUNT","ABRECHUNG"
26600 DATA "CONNECT","VERBINDEN"
26610 DATA "FIRST","ERSTER"
26620 DATA "FACTOR","FAKTOR"
26630 DATA "DEFINE","DEFINIEREN"
26640 DATA "DIMENSION","DIMENSION"
26650 DATA "FIND","FINDEN"
26660 DATA "SECTOR","SEKTOR"
26670 DATA "FULL","VOLL"
26680 DATA "SEQUENTIAL","DER REIHE NACH"
26690 DATA "RESTORE","WIEDERHERSTELLEN"
26700 DATA "RELOCATION","VESCHIEBUNG"
26710 DATA "PARITY","PARITAET"
26720 DATA "EXCHANGE","AUSTAUSCHEN"
26730 DATA "REQUEST","ANFORDERUNG"
26740 DATA "SELECT","AUSWAEHLEN"
26750 DATA "EVENT","EREIGNIS"
26760 DATA "FAST","SCHNELL"
26770 DATA "SLOW","LANGSAM"
26780 DATA "DYNAMIC","DYNAMISCH"
26790 DATA "DIRECT","DIREKT"
26800 DATA "COMMON","ALLGEMEIN"
26810 DATA "CORE","KERN"
26820 DATA "LOGICAL","LOGISCH"
26830 DATA "INVERTER","UMKEHRER"
26840 DATA "CROSS","KREUZUNG"
26850 DATA "LAST","LETZTER"
26860 DATA "HEAD","KOPF"
26870 DATA "HOST","WIRT"
26880 DATA "LONG","LANG"
26890 DATA "LARGE","GROSS"
26900 DATA "ARRAY","MATRIX"
26910 DATA "EXTENDED","ERWEITERT"
26920 DATA "EXPANDED","AUSGEDEHNT"
26930 DATA "ENHANCED","ANGEREICHERT"
26940 DATA "ADVANCED","FORTGESCHRITTEN"
26950 DATA "EXTERNAL","EXTERN"
26960 DATA "REGISTER","REGISTER"
26970 DATA "AUXILIARY","ZUSAETZLICH"
26980 DATA "IDENTIFIER","BEZEICHNER"
26990 DATA "INDICATOR","FUNKTIONSANZEIGE"
27000 DATA "INDIRECT","INDIREKT"
27010 DATA "INTERFACE","SCHNITTSTELLE"
27020 DATA "INTERNAL","INTERN"
27030 DATA "INSTRUCTION","BEFEHL"
27040 DATA "NEGATIVE","NEGATIV"
27050 DATA "OVERLOAD","UEBERLAST"
27060 DATA "OVERFLOW","UEBERLAUF"
27070 DATA "ACKNOWLEDGE","BESTAETIGUNG"
27080 DATA "CONVERSION","UMWANDLUNG"
27090 DATA "INITIAL","ANFAENGLICH"
27100 DATA "QUERY","ABFRAGE"
27110 DATA "RANDOM","ZUFAELLIG"
27120 DATA "RELEASE","ENTLASSEN"
27130 DATA "NETWORK","NETZWERK"
27140 DATA "STATE","STATUS"
27150 DATA "GRAPHIC","GRAFISCH"
27160 DATA "REMARK","BEMERKUNG"
27170 DATA "PUSH","AUSLAGERN"
27180 DATA "POP","ZURUECKHOLEN"
27190 DATA "AND","UND","OR","ODER"
27200 DATA "DISABLE","GESPERRT"
27210 DATA "ENABLE","FEIGEGEBEN"
27220 DATA "JUMP","SPRINGEN"
27230 DATA "RELATIVE","RELATIV"
27240 DATA "RESTART","WIEDERANLAUF"
27250 DATA "CONDITION","BEDINGUNG"
27260 DATA "MODE","BETRIEBSART"
27270 DATA "ROTATE","ROTIEREN"
27280 DATA "BUS","SAMMELLEITUNG"
27290 DATA "KEY","TASTE"
27300 DATA "LABEL","MARKE"
27310 DATA "MASK","MASKE"
27320 DATA "MNEMONIC","KURZWORT"
27330 DATA "REFRESH","AUFFRISCHEN"
27340 DATA "POINTER","ZEIGER"
27350 DATA "VOLATILE","FLUECHTIG"
27360 DATA "WRITE","SCHREIBEN"
27370 DATA "COUNTER","ZAEHLER"
27380 DATA "FETCH","HOLEN"
27390 DATA "SIGN","VORZEICHEN"
27400 DATA "ADJUST","AUSGLEICH"
27410 DATA "HOLD","HALTEN"
27420 DATA "RECALL","WIEDERAUFRUF"
27430 DATA "ODD","UNGERADE","EVEN","GERADE"
27440 DATA "OVER","UEBER"
27450 DATA "UP","HINAUF","DOWN","HINAB"
27460 DATA "LATCH","FANGEN"
27470 DATA "TRANSMIT","SENDEN"
27480 DATA "RECEIVE","EMPFANGEN"
27490 DATA "HANDSHAKING","QUITTUNGSBETRIEB"
27500 DATA "TRIGGER","AUSLOESER"
27510 DATA "FIRMWARE","HERSTELLERPROGRAMME"
27520 DATA "CREATE","ERSTELLEN"
27530 DATA "APPEND","ANHAENGEN"
27540 DATA "EDIT","TEXT BEARBEITEN"
27550 DATA "DRIVER","TREIBER"
27560 DATA "TIMER","ZEITGEBER"
27570 DATA "MANUAL","HANDBUCH"
27580 DATA "CURRENT","STROM"
27590 DATA "VOLTAGE","SPANNUNG"
27600 DATA "SUPPLY","VERSORGUNG"
27610 DATA "SINGLE","EINFACH"
27620 DATA "INTEGER","GANZZAHLIG"
27630 DATA "CIRCLE","KREIS"
27640 DATA "PIXEL","BILDPUNKT"
27650 DATA "DRAW","ZEICHNEN"
27660 DATA "PAPER","PAPIER"
27670 DATA "SHEET","BLATT"
27680 DATA "FILL","FUELLEN"
27690 DATA "RECTANGLE","RECHTECK"
27700 DATA "PLOTTER","ZEICHENGERAET"
27710 DATA "SCREEN","BILDSCHIRM"
27720 DATA "FORMFEED","SEITENVORSCHUB"
27730 DATA "QUIT","VERLASSEN"
27740 DATA "*","*"
32000 REM       (C) BIDAT 10.6.89
32010 REM AUTOR:BERTHOLD BIENER
32020 REM       R.-EYERMANN-RING4/0106
32030 REM       ERFURT
32040 REM       5063
32050 REM AUF Z9001 FUER RADIO DDR 2
32060 REM REV 8.1.90 BIDAT
32070 REM
32080 REM Erstausstrahlung: Radio DDR
32090 REM -spezial 900302
