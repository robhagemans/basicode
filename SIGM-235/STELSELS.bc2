1000 A=50:GOTO20:REM RESERVEER STRINGRUIMTE
1010 GOSUB5000:REM UITLEG
1015 GOSUB100
1020 J=0:K=0:Q=0:I=0:D=0
1030 PRINT"* Oplossen LINEAIR STELSEL *":PRINT
1040 PRINT"ORDE ";:INPUTN
1050 M=N-1
1060 DIMA(M,N+1):PRINT
1070 FORK=0TON
1080 :IFK=NTHENPRINT"vektor";:GOTO1100
1090 :PRINT"kolom";K+1;
1100 :FORJ=0TOM
1110 ::GOSUB120:HO=13:GOSUB110
1120 ::PRINT"#";J+1;
1130 ::INPUTA(J,K)
1140 :NEXTJ:PRINT
1150 NEXTK
1200 FORI=0TOM:A(I,N+1)=I:NEXT
1210 D=1
1220 FORK=0TON-2
1230 :I=K:Q=ABS(A(K,K))
1240 :FORJ=1TOM-K
1250 ::R=ABS(A(J+K,K))
1260 ::IFQ<RTHENQ=R:I=K+J
1270 :NEXTJ
1280 :IFI=KTHEN1330
1290 :D=-D
1300 :FORJ=0TON+1
1310 ::Q=A(K,J):A(K,J)=A(I,J):A(I,J)=Q
1320 :NEXTJ
1330 :D=D*A(K,K):IFD=0THEN1420
1340 :FORI=MTOK+1 [opcode d1] -1
1350 ::Q=A(I,K)/A(K,K)
1360 ::A(I,K)=Q
1370 ::FORJ=K+1TOM
1380 :::A(I,J)=A(I,J)-Q*A(K,J)
1390 ::NEXTJ
1400 :NEXTI
1410 NEXTK
1420 D=D*A(M,M)
1430 PRINT"determinant= "D:PRINT
1440 IFD=0THENPRINT"geen oplossing":PRINT:GOTO2500
1450 :
1500 FORK=0TOM
1510 :Q=A(K,N)
1520 :J=0
1530 :IFJ<KTHENQ=Q-A(J,N)*A(K,J):J=J+1:GOTO1530
1540 :A(K,N)=Q
1550 NEXTK
1560 FORK=MTO0 [opcode d1] -1
1570 :Q=A(K,N)
1580 :J=M
1590 :IFJ>KTHENQ=Q-A(J,N)*A(K,J):J=J-1:GOTO1590
1600 :A(K,N)=Q/A(K,K)
1610 NEXTK
1620 :
2000 FORI=0TOM
2010 :SR=I+1:GOSUB300
2020 :PRINT"x";SR$;" = ";A(I,N)
2030 NEXTI
2040 :
2100 PRINT:PRINT"Wilt u een andere vektor ";
2110 GOSUB4000
2120 IFA$="n"ORA$="N"THEN2500
2130 PRINT"Vektor";
2140 FORK=0TOM
2150 :I=0
2160 :IFA(I,N+1)<>KTHENI=I+1:GOTO2160
2170 :GOSUB120:HO=13:GOSUB110
2180 :PRINT"#";K+1;
2190 :INPUTA(I,N)
2200 NEXT
2210 PRINT:GOTO1500
2220 :
2500 PRINT"Wilt u een andere matrix ";
2510 GOSUB4000
2520 IFA$="n"ORA$="N"THENEND
2530 RUN
2540 :
4000 INPUTA$
4010 A$=LEFT$(A$,1)
4020 PRINT:RETURN
4030 :
5000 GOSUB100
5010 PRINT"* Oplossen van een LINEAIR STELSEL *":PRINT
5020 PRINT"Als voorbeeld een stelsel van ORDE 3 :"
5030 PRINT
5040 PRINT" (   2 x1 + 3 x2 -   x3 =  5"
5050 PRINT" (  -3 x1 +   x2 + 2 x3 =  5"
5060 PRINT" (     x1        -   x3 = -2"
5070 PRINT
5080 PRINT"De orde van dit stelsel is 3 omdat er"
5090 PRINT"drie vergelijkingen met drie onbekenden"
5100 PRINT"zijn."
5110 PRINT
5120 PRINT"In dit stelsels komen vier KOLOMMEN of"
5130 PRINT"ook wel VECTOREN voor, namelijk de "
5140 PRINT"kolom getallen voor de variabele x1, de"
5150 PRINT"kolom voor de x2, de kolom voor de x3,"
5160 PRINT"en tenslotte de kolom (ofwel de VEKTOR)"
5170 PRINT"getallen rechts van het = teken."
5180 GOSUB6000
5200 GOSUB100
5210 PRINT"Voor het oplossen van dit stelsel:"
5220 PRINT
5230 PRINT" (   2 x1 + 3 x2 -   x3 =  5"
5240 PRINT" (  -3 x1 +   x2 + 2 x3 =  5"
5250 PRINT" (     x1        -   x3 = -2"
5260 PRINT
5270 PRINT"moet u mij eerst de orde opgeven en"
5280 PRINT"vervolgens de vier kolommen."
5285 PRINT:PRINT
5290 PRINT"In dit voorbeeld dus ORDE=3"
5295 PRINT
5300 PRINT"kolom 1:   2 , -3 ,  1"
5310 PRINT"kolom 2:   3 ,  1 ,   0"
5325 PRINT
5330 PRINT"Vektor :   5 ,  5 , -2"
5340 PRINT
5350 PRINT"Daarna bereken ik de juiste waarden"
5360 PRINT"voor x1, x2 en x3 en meldt ze."
5370 GOSUB6000
5380 RETURN
5390 :
6000 HO=0:VE=24:GOSUB110
6010 PRINT"druk op de spatiebalk voor het vervolg";
6020 GOSUB210:IFIN$<>" "THEN6020
6030 RETURN
6040 :
10000 REM
10010 REM" LINEAIRE STELSELS OPLOSSEN
10020 REM" ==========================
10030 REM
10040 REM  DEZE VERSIE NOVEMBER 1982
10050 REM"  bewerkt voor BASICODE-2
10060 REM"     door leden van de
10070 REM"       BASICODE-groep
10080 REM
10090 REM
10100 REM"Oorspronkelijke versie uit
10110 REM"198,0 destijds ingezonden
10120 REM"voor de HOBBYSCOOP-wedstrijd
10130 REM
10140 REM
10150 REM           DOOR :
10160 REM
10170 REM"     P.J.H. van  LIS
10180 REM"      Hereweg 86-2a
10190 REM"    9725 AH  GRONINGEN
10200 REM"   ====================
10210 REM
